# Contributions from ArchLinux: https://git.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/gcr

pkgname=gcr
pkgver=3.20.0+45+g4708f35
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
url="https://git.gnome.org/browse/gcr"
arch=(x86_64)
license=(GPL2)
depends=(dconf gtk3 libgcrypt p11-kit)
makedepends=(intltool gobject-introspection python3 vala libxslt gnome-common git gtk-doc)
checkdepends=(python2)
_commit=4708f3562ddf28bf51eb70b9cff548b217370639  # master
source=("git+https://git.gnome.org/browse/gcr#commit=$_commit"
        10-gcr.conf)
sha256sums=('SKIP'
            '1230351a1da53a9c2bada81c44f5a5113e5457552196eccb7b1413dcf4f661b5')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/gcr \
    --enable-gtk-doc \
    --disable-static \
    --disable-update-mime \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  PATH="$srcdir/path:$PATH" dbus-run-session make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # gcr wants to lock some memory to prevent swapping out private keys
  # https://bugs.archlinux.org/task/32616
  # https://bugzilla.gnome.org/show_bug.cgi?id=688161
  install -Dm644 ../10-gcr.conf "$pkgdir/etc/security/limits.d/10-gcr.conf"
}
