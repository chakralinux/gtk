# Contributor from CCR:  danyf90 <daniele.formichelli@gmail.com>

pkgname=gegl
pkgver=0.4.8
pkgrel=1
pkgdesc="Graph based image processing framework"
arch=('x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'gtk2' 'libspiro' 'json-glib' 'libgexiv2' 'libraw' 'openexr' 'librsvg' 'libtiff' 'lua' 'jasper' 'lensfun' 'suitesparse')
makedepends=('intltool' 'ruby' 'mesa' 'glu' 'ffmpeg' 'exiv2' 'vala' 'python2' 'gobject-introspection')
optdepends=('ffmpeg: for using the ffmpeg plugin')
source=("http://ftp.gimp.org/pub/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('719468eec56ac5b191626a0cb6238f3abe9117e80594890c246acdc89183ae49')

build() {
  cd $pkgname-$pkgver
  ./configure  --prefix=/usr \
               --with-sdl \
               --with-openexr \
               --with-librsvg \
               --with-libavformat \
               --with-jasper \
               --disable-docs \
               --enable-introspection

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}

