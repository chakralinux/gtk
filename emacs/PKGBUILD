pkgname=emacs
pkgver=24.3
pkgrel=3
pkgdesc="The extensible, customizable, self-documenting real-time display editor"
arch=('x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('librsvg' 'gpm' 'giflib' 'libxpm' 'libotf' 'm17n-lib' 'gtk3' 'hicolor-icon-theme' 'desktop-file-utils' 'alsa-lib' 'imagemagick' 'gnutls')
install=emacs.install
source=(ftp://ftp.gnu.org/gnu/emacs/$pkgname-$pkgver.tar.xz{,.sig})
md5sums=('ea9ed000ca165280265aabb55b9afbd7'
         '3f6990fabfbe1d5f3cd58bace4eb20f1')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/extra/usr --sysconfdir=/extra/etc --libexecdir=/extra/usr/lib \
    --localstatedir=/var --with-x-toolkit=gtk3 --with-xft
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # remove conflict with ctags package
  mv "$pkgdir"/extra/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/extra/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

  # remove conflict with texinfo
  rm "$pkgdir"/extra/usr/share/info/info.info.gz
  # fix user/root permissions on usr/share files
  find "$pkgdir"/extra/usr/share/emacs/$pkgver -exec chown root:root {} \;
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
}
