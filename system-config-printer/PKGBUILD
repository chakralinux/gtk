pkgname="system-config-printer"
pkgver=1.5.6
pkgrel=1
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('x86_64')
license=('GPL')
depends=('python3-pycups' 'python3-dbus' 'python3-pycurl' 'python3-requests' 'python3-gobject' 'gtk3'
         'python3-cairo')
makedepends=('intltool' 'xmlto' 'docbook-xsl' 'desktop-file-utils')
optdepends=('libgnome-keyring: password management'
            'pysmbc: SMB browser support')
source=(http://cyberelk.net/tim/data/$pkgname/1.5/$pkgname-$pkgver.tar.xz)
md5sums=('c3b549519e2d88f2ae12bd50831bf38a')


build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udevdir=/usr/lib/udev --with-udev-rules

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # Compile *.pyc
  find "${pkgdir}" -name '*.py' -exec python3 -mpy_compile {} +
  # Compile *.pyo
  find "${pkgdir}" -name '*.py' -exec python3 -O -mpy_compile {} +
  
  # new cups 2.0.0 service naming
  sed -i "s|cups.socket|org.cups.cupsd.socket|g" ${pkgdir}/usr/lib/systemd/system/configure-printer@.service
}
