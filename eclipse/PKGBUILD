pkgbase=eclipse
pkgname=(eclipse-{java,cpp,php,jee})
pkgver=4.6.0
pkgrel=1
_release=mars
_rel=1
pkgdesc="Highly extensible IDE"
arch=('x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'libxtst')
optdepends=('libwebkit-gtk2: for in app manual')
install=${pkgbase}.install
license=("EPL/1.0")
source=("http://eclipse.mirror.garr.it/mirrors/eclipse//technology/epp/downloads/release/$_release/$_rel/eclipse-java-$_release-$_rel-linux-gtk-x86_64.tar.gz"
        "http://eclipse.mirror.garr.it/mirrors/eclipse//technology/epp/downloads/release/$_release/$_rel/eclipse-cpp-$_release-$_rel-linux-gtk-x86_64.tar.gz"
        "http://eclipse.mirror.garr.it/mirrors/eclipse//technology/epp/downloads/release/$_release/$_rel/eclipse-php-$_release-$_rel-linux-gtk-x86_64.tar.gz"
        "http://eclipse.mirror.garr.it/mirrors/eclipse//technology/epp/downloads/release/$_release/$_rel/eclipse-jee-$_release-$_rel-linux-gtk-x86_64.tar.gz"
        'eclipse-java.desktop'
        'eclipse-cpp.desktop'
        'eclipse-php.desktop'
        'eclipse-jee.desktop'
        'eclipse.svg'
        'epl-v10.html'
        'eclipse.sh')
noextract=("eclipse-java-$_release-$_rel-linux-gtk-x86_64.tar.gz"
           "eclipse-cpp-$_release-$_rel-linux-gtk-x86_64.tar.gz"
           "eclipse-php-$_release-$_rel-linux-gtk-x86_64.tar.gz"
           "eclipse-jee-$_release-$_rel-linux-gtk-x86_64.tar.gz")
sha1sums=('74cbc500a4ade717e115afb13b3a633f8a4db786'
          'acb089bac953232ac1004fee4d7f3b7b84aad68f'
          'ad3a07f4b2ebd29fc4a4f2bf5314cef59bdd075c'
          '503bdce0ec918265821b94949880fd26bd783e68'
          '8272587b38fb823aed510a75dfe1d91d249ae74f'
          '67dc0d49a668e61087d24c89999c5f88eccb2e71'
          '838df3cbc1ece133f81a0677636fe2b793cb9b02'
          'adeb6355fa963102126bf653e87b6fed5c18a1a0'
          '91206140df8c2039bb29fc9e67f067f1b07003a4'
          '35666c54f2406125707e63edab12f2914d85ca76'
          '05d119f25c30f6703f624ae836a3f1d9cd8fa800')


prepare() {
   bsdtar -xf eclipse-java-$_release-$_rel-linux-gtk-x86_64.tar.gz
   mv eclipse eclipse-java
   bsdtar -xf eclipse-cpp-$_release-$_rel-linux-gtk-x86_64.tar.gz
   mv eclipse eclipse-cpp
   bsdtar -xf eclipse-php-$_release-$_rel-linux-gtk-x86_64.tar.gz
   mv eclipse eclipse-php
   bsdtar -xf eclipse-jee-$_release-$_rel-linux-gtk-x86_64.tar.gz
   mv eclipse eclipse-jee
}            
            
package_eclipse-java() {
  conflicts=('eclipse')
  provides=('eclipse')
  cd ${srcdir}

  # create directories
  install -dm755 "${pkgdir}"/usr/{share/{applications,eclipse-java,licenses/eclipse-java},bin}
  
  # install bin symlink
  install -D eclipse.sh ${pkgdir}/usr/bin/eclipse-java
  sed -i s!'/usr/share/eclipse'!'/usr/share/eclipse-java'! \
  ${pkgdir}/usr/bin/eclipse-java
  
  # install .desktop and svg
  install -Dm644 "${srcdir}/eclipse-java.desktop" "${pkgdir}/usr/share/applications/eclipse-java.desktop"
  install -Dm644 "${srcdir}/eclipse.svg" "${pkgdir}/usr/share/pixmaps/eclipse-java.svg"
  
  # install eclipse
  cp -ar "${srcdir}/eclipse-java" "${pkgdir}/usr/share/"

  # license
  cp -a "${srcdir}/epl-v10.html" "${pkgdir}/usr/share/licenses/eclipse-java/epl-v10.html"
}

package_eclipse-cpp() {
  conflicts=('eclipse')
  provides=('eclipse')
  pkgdesc+=" - C/C++"
  cd ${srcdir}

  # create directories
  install -dm755 "${pkgdir}"/usr/{share/{applications,eclipse-cpp,licenses/eclipse-cpp},bin}
  
  # install bin symlink
  install -D eclipse.sh ${pkgdir}/usr/bin/eclipse-cpp
  sed -i s!'/usr/share/eclipse'!'/usr/share/eclipse-cpp'! \
  ${pkgdir}/usr/bin/eclipse-cpp
  
  # install .desktop and svg
  install -Dm644 "${srcdir}/eclipse-cpp.desktop" "${pkgdir}/usr/share/applications/eclipse-cpp.desktop"
  install -Dm644 "${srcdir}/eclipse.svg" "${pkgdir}/usr/share/pixmaps/eclipse-cpp.svg"
  
  # install eclipse
  cp -ar "${srcdir}/eclipse-cpp" "${pkgdir}/usr/share/"

  # license
  cp -a "${srcdir}/epl-v10.html" "${pkgdir}/usr/share/licenses/eclipse-cpp/epl-v10.html"
}

package_eclipse-php() {
  conflicts=('eclipse')
  provides=('eclipse')
  pkgdesc+=" - PHP"
  depends+=('php')
  cd ${srcdir}

  # create directories
  install -dm755 "${pkgdir}"/usr/{share/{applications,eclipse-php,licenses/eclipse-php},bin}
  
  # install bin symlink
  install -D eclipse.sh ${pkgdir}/usr/bin/eclipse-php
  sed -i s!'/usr/share/eclipse'!'/usr/share/eclipse-php'! \
  ${pkgdir}/usr/bin/eclipse-php
  
  # install .desktop and svg
  install -Dm644 "${srcdir}/eclipse-php.desktop" "${pkgdir}/usr/share/applications/eclipse-php.desktop"
  install -Dm644 "${srcdir}/eclipse.svg" "${pkgdir}/usr/share/pixmaps/eclipse-php.svg"
  
  # install eclipse
  cp -ar "${srcdir}/eclipse-php" "${pkgdir}/usr/share/"

  # license
  cp -a "${srcdir}/epl-v10.html" "${pkgdir}/usr/share/licenses/eclipse-php/epl-v10.html"
}

package_eclipse-jee() {
  conflicts=('eclipse')
  provides=('eclipse')
  pkgdesc+=" - Java Enterprise Edition"
  cd ${srcdir}

  # create directories
  install -dm755 "${pkgdir}"/usr/{share/{applications,eclipse-jee,licenses/eclipse-jee},bin}
  
  # install bin symlink
  install -D eclipse.sh ${pkgdir}/usr/bin/eclipse-jee
  sed -i s!'/usr/share/eclipse'!'/usr/share/eclipse-jee'! \
  ${pkgdir}/usr/bin/eclipse-jee
  
  # install .desktop and svg
  install -Dm644 "${srcdir}/eclipse-jee.desktop" "${pkgdir}/usr/share/applications/eclipse-jee.desktop"
  install -Dm644 "${srcdir}/eclipse.svg" "${pkgdir}/usr/share/pixmaps/eclipse-jee.svg"
  
  # install eclipse
  cp -ar "${srcdir}/eclipse-jee" "${pkgdir}/usr/share/"

  # license
  cp -a "${srcdir}/epl-v10.html" "${pkgdir}/usr/share/licenses/eclipse-jee/epl-v10.html"
}
