
pkgname=eclipse
pkgver=4.4.2
pkgrel=1
_release=luna-SR2
pkgdesc="An IDE for Java and other languages."
arch=('x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'libwebkit-gtk2' 'libxtst')
install=${pkgname}.install
license=("EPL/1.0")

# The file eclipse.png in this directory was fetched from:
#
#   http://download.eclipse.org/eclipse/eclipse/downloads/drops4/R-${pkgver}-${_date}/news/images/icon.png

source=("http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/epp/downloads/release/${_release/-//}/${pkgname}-standard-${_release}-linux-gtk-x86_64.tar.gz"
        'eclipse.png'
        'eclipse.desktop')
sha256sums=('835a2aaa7f3c81d4c307c17414bdd4a5b219035777eb42150e1d46f5465db98c'
            '91eb03b8ff33e2b05b6c0b1fa5ff145666833674ba3bd2aa2732fa0a3651c03d'
            '88daed5095d908a4f4c273aafcd2855899f2516766a8958b9a653e030daa173c')

package() {
  cd ${pkgname}

  # create directories
  install -dm755 "${pkgdir}"/usr/{share/{applications,licenses/eclipse},bin}
  
  # install eclipse
  cp -ar "${srcdir}/eclipse" "${pkgdir}/usr/share/"

  # install bin symlink
  ln -s "/usr/share/eclipse/eclipse" "$pkgdir/usr/bin/"
  install -m644 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/"
  
  # install icon
  for _i in 16 32 48 256; do
    install -Dm644 plugins/org.eclipse.platform_*/eclipse${_i}.png \
      "${pkgdir}/usr/share/icons/hicolor/${_i}x${_i}/apps/eclipse.png"
  done

  install -Dm644 "${srcdir}/eclipse.png" \
                 "${pkgdir}/usr/share/icons/hicolor/512x512/apps/eclipse.png"

  # license
  cp -a epl-v10.html "${pkgdir}/usr/share/licenses/eclipse/epl-v10.html"
}
