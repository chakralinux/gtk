
pkgname=eclipse
pkgver=4.3.2
pkgrel=1
_release=kepler-SR2
pkgdesc="An IDE for Java and other languages."
arch=('x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'libwebkit-gtk2' 'libxtst')
install=${pkgname}.install
makedepends=('zip')
license=("EPL/1.0")

# The file eclipse.png in this directory was fetched from:
#
#   http://download.eclipse.org/eclipse/eclipse/downloads/drops4/R-${pkgver}-${_date}/news/images/icon.png

source=("http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/epp/downloads/release/${_release/-//}/$pkgname-standard-$_release-linux-gtk-x86_64.tar.gz"
        'eclipse.png'
        'eclipse.desktop')
md5sums=('b72d5a6caae4e2fd86be2f2f134fc8cb'
         '04e05dda343b1b0c521a1e094e69f98b'
         'ba8a37e30a7ebd69774cec87c69e8c44')

package() {
  cd "${srcdir}/${pkgname}"

  # install eclipse
  install -d -m755 "${pkgdir}/usr/share"
  cp -ar "${srcdir}/eclipse" "${pkgdir}/usr/share/"

  # install misc
  install -d "${pkgdir}/usr/bin" \
             "${pkgdir}/usr/share/applications"

  # install bin symlink
  ln -s "/usr/share/eclipse/eclipse" "$pkgdir/usr/bin/"
  install -m644 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/"
  
  # install icon
  for _i in 16 32 48 256; do
    install -Dm644 plugins/org.eclipse.platform_*/eclipse${_i}.png \
      "$pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/eclipse.png"
  done

  install -D -m644 "${srcdir}/eclipse.png" \
                   "${pkgdir}/usr/share/icons/hicolor/512x512/apps/eclipse.png"

  # license
  install -d -m755 "${pkgdir}/usr/share/licenses/eclipse/"
  cp -a epl-v10.html "$pkgdir/usr/share/licenses/eclipse/epl-v10.html"
}
