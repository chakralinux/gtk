pkgname=ardour
pkgver=3.5.357
pkgrel=1
pkgdesc="Professional-grade digital audio workstation"
arch=('x86_64')
url="http://ardour.org/"
license=('GPL')
depends=('liblrdf' 'liblo' 'libsmf' 'lilv'
         'aubio' 'libgnomecanvasmm' 'suil')
makedepends=('python2' 'boost' 'cppunit' 'doxygen' 'graphviz')
install=$pkgname.install
source=(${pkgname}-${pkgver}::"git://git.ardour.org/${pkgname}/${pkgname}.git#tag=${pkgver}"
        "$pkgname.desktop")
md5sums=('SKIP'
         '3755e6f9518fae4c47927cf615998262')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  #sed -i 's/python ${SRC}/python2 ${SRC}/' libs/pbd/wscript
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --docs

  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # install some freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
