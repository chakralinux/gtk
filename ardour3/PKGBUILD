pkgname=ardour
_pkgname=Ardour3
pkgver=3.5.143
_pkgver=3.5
pkgrel=1
pkgdesc="Professional-grade digital audio workstation"
arch=('x86_64')
url="http://ardour.org/"
license=('GPL')
depends=('liblrdf' 'liblo' 'libsmf' 'lilv'
         'aubio' 'libgnomecanvasmm' 'suil')
makedepends=('python2' 'boost' 'cppunit' 'doxygen' 'graphviz')
install=$pkgname.install
source=("ftp://ftp.archlinux.org/other/$pkgname/${_pkgname}-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('52c3b8ea3ab014002e84e377f8a15097'
         '3755e6f9518fae4c47927cf615998262')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"

  # python2 fix
  #sed -i 's/python ${SRC}/python2 ${SRC}/' libs/pbd/wscript
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --docs

  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # install some freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
