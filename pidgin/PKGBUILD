pkgname=('pidgin')
pkgver=2.12.0
pkgrel=1
arch=('x86_64')
pkgdesc="Multi-protocol instant messaging client"
url="http://pidgin.im/"
license=('GPL')
depends=('libpurple' 'startup-notification' 'gtkspell' 'libxss' 'libsm'
         'hicolor-icon-theme' 'farstream')
makedepends=('nss' 'libsasl' 'libidn' 'python3' 'avahi'
             'tk' 'ca-certificates' 'intltool' 'networkmanager')
optdepends=('aspell: for spelling correction')
install=pidgin.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        'pidgin-2.10.12-py3-fixes.patch')
md5sums=('8287400c4e5663e0e7844e868d5152af'
         '83509c89a24a9c675ea6d876ef693bd5')

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   patch -Np1 -i ../pidgin-2.10.12-py3-fixes.patch
   autoreconf -vi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-meanwhile \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python3 \
    --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
  cd "$srcdir/pidgin-$pkgver"

  # For linking
  make -C libpurple DESTDIR="$pkgdir" install-libLTLIBRARIES

  make -C pidgin DESTDIR="$pkgdir" install
  make -C doc DESTDIR="$pkgdir" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="$pkgdir" uninstall-libLTLIBRARIES
}

