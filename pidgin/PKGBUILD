pkgname=('pidgin')
pkgver=2.10.11
pkgrel=1
arch=('x86_64')
pkgdesc="Multi-protocol instant messaging client"
url="http://pidgin.im/"
license=('GPL')
depends=('libpurple' 'startup-notification' 'gtkspell' 'libxss' 'libsm'
         'hicolor-icon-theme')
makedepends=('nss' 'libsasl' 'libidn' 'python2' 'farstream' 'avahi'
             'tk' 'ca-certificates' 'intltool' 'networkmanager')
optdepends=('aspell: for spelling correction')
install=pidgin.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        'libpurple-gst1.patch')
md5sums=('7c8bb6f997e05e7929104439872c2844'
         '99164fd9fbca352cae70ae43bae47546')

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   patch -Np1 -i $srcdir/libpurple-gst1.patch
   autoreconf -vi
   
   # Use Python 2
   sed -i 's/env python$/&2/' */plugins/*.py \
     libpurple/purple-{remote,notifications-example,url-handler}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-meanwhile \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python2 \
    --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
  cd "$srcdir/pidgin-$pkgver"

  # For linking
  make -C libpurple DESTDIR="$pkgdir" install-libLTLIBRARIES

  make -C pidgin DESTDIR="$pkgdir" install
  make -C doc DESTDIR="$pkgdir" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="$pkgdir" uninstall-libLTLIBRARIES
}
