pkgname=pygtk
pkgver=2.24.0
pkgrel=1
pkgdesc="Python bindings for the GTK widget set"
arch=('x86_64')
license=('LGPL')
depends=('libglade' 'python2-cairo' 'python2-gobject')
makedepends=('python-numpy')
optdepends=('python-numpy')
options=('!libtool')
url="http://www.pygtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        python27.patch)
sha256sums=('cd1c1ea265bd63ff669e92a2d3c2a88eb26bcd9e5363e0f82c896e649f206912'
            '39a30456cba055a452bb55c74ef1ff2f5f7bfaad22855b4dd569ab009b56b682')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  #https://bugzilla.gnome.org/show_bug.cgi?id=623965
  patch -Np1 -i "${srcdir}/python27.patch"

  PYTHON=/usr/bin/python2 ./configure --prefix=/extra/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 gtk/gtk-extrafuncs.defs "${pkgdir}/extra/usr/share/pygtk/2.0/defs/"

  sed -i -e 's#env python$#env python2#' "${pkgdir}"/extra/usr/lib/pygtk/2.0/{,demos/}*.py
}
