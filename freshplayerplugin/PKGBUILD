pkgname=freshplayerplugin
pkgver=0.3.9
pkgrel=1
pkgdesc='PPAPI-host NPAPI-plugin adapter.'
arch=('x86_64')
url='https://github.com/i-rinat/freshplayerplugin'
license=('MIT')
depends=('pepperflashplugin' 'pango' 'alsa-lib' 'freetype2' 'cairo' 'glib2' 'libpulse' 
         'libx11' 'libevent' 'gtk3' 'libgl' 'openssl' 'ffmpeg' 'libxrandr'
         'libxrender' 'libxcursor' 'v4l-utils' 'libva' 'jack' 'icu')
makedepends=('cmake' 'ragel')
conflicts=('freshplayerplugin-git' 'flashplugin' 'chromium-pepper-flash')
source=("$url/archive/v$pkgver.tar.gz"
        "ffmpeg4.patch::https://github.com/i-rinat/freshplayerplugin/commit/934aa9c24d34f8203744b56e4ac6e8599446ca02.patch"
        'freshwrapper.conf')
backup=('etc/freshwrapper.conf')
md5sums=('a6ccf674b64e250b334ffcb748dd2317'
         'b4171440e26abb0049f151b98ebd2f63'
         'f3575ad86d10bb361c3530027cacc84e')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../ffmpeg4.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  for lib in libfreshwrapper-*.so; do
    install -Dm755 $lib "$pkgdir"/usr/lib/mozilla/plugins/$lib
  done
  
  #Install config files.
  install -Dm644 $srcdir/freshwrapper.conf "$pkgdir/etc/freshwrapper.conf"
  install -Dm644 $srcdir/$pkgname-$pkgver/data/freshwrapper.conf.example \
                 "$pkgdir/etc/freshwrapper.conf.example"
}
