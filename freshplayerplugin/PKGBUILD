# Maintainer: AlmAck
# Contributor:  ahjolinna <ahjolinna@ovi.com>

pkgname=freshplayerplugin
pkgver=0.2.3
pkgrel=1
pkgdesc='PPAPI-host NPAPI-plugin adapter.'
arch=('x86_64')
url='https://github.com/i-rinat/freshplayerplugin'
license=('MIT')
depends=('pepperflashplugin' 'pango' 'alsa-lib' 'freetype2' 'cairo' 'glib2' 'libpulse' 'libx11' 'libconfig' 'libevent' 'gtk2' 'libgl')
makedepends=('cmake' 'ragel')
conflicts=('freshplayerplugin-git' 'flashplugin' 'chromium-pepper-flash')
source=("$url/archive/v$pkgver.tar.gz"
        'freshwrapper.conf')
backup=('etc/freshwrapper.conf')
md5sums=('c2b2a4440a1393961f4559e3f9032256'
         'f5c37482ff9cb4c0db00e12050ed6539')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 LICENSE.MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  for lib in libfreshwrapper-*.so; do
    install -Dm755 $lib "$pkgdir"/usr/lib/mozilla/plugins/$lib
  done

  install -Dm644 $srcdir/freshwrapper.conf "$pkgdir/etc/freshwrapper.conf"
}
