pkgname=ardour
pkgver=5.4
pkgrel=1
pkgdesc="Professional-grade digital audio workstation"
arch=('x86_64')
url="http://ardour.org/"
license=('GPL')
depends=('liblrdf' 'liblo' 'lilv' 'gtkmm' 'aubio' 'suil' 'taglib' 'rubberband')
makedepends=('git' 'python2' 'boost' 'cppunit' 'doxygen' 'graphviz')
install=$pkgname.install
source=(${pkgname}-${pkgver}::"git://git.ardour.org/${pkgname}/${pkgname}.git#tag=${pkgver}"
        "$pkgname.desktop")
sha256sums=('SKIP'
         '985b80c65c122be924eefcc6e4dc13498ace9da6151f79c860c794a0c02ead7b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i 's/python ${SRC}/python2 ${SRC}/' libs/pbd/wscript
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --with-backends="jack,alsa" \
                        --libjack=weak \
                        --optimize \
                        --docs

  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # install some freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
