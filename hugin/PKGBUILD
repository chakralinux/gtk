# WARNING: this package should exactly match the hugin-tools platform package

pkgname=hugin
pkgver=2014.0.0
pkgrel=1
pkgdesc='Panorama photo stitcher'
url='http://hugin.sourceforge.net/'
license=('GPL')
arch=('x86_64')
depends=('wxgtk' 'boost-libs' 'enblend-enfuse' 'exiv2' "hugin-tools=${pkgver}"
         'lensfun' 'lapack' 'make' 'perl-exiftool' 'desktop-file-utils')
makedepends=('cmake' 'boost' 'tclap' 'mesa')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('f098aa0ede44010d3bb2bb38693177533fd776c45063a338c4c483d7e471ec29')

install=install

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix compiling against lensfun-0.3.0
  sed '/LF_DIST_MODEL_FOV1/d' -i src/hugin_base/lensdb/LensDB.cpp
}

build() {
	cd ${pkgname}-${pkgver}
	
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LAPACK=yes \
		-DBUILD_HSI=no
	make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  msg "Removing duplicated binaries in hugin-tools"
  rm -v "${pkgdir}/usr/share/hugin/data/celeste.model"
  rm -v "${pkgdir}/usr/bin/autooptimiser"
  rm -v "${pkgdir}/usr/bin/hugin_hdrmerge"
  rm -v "${pkgdir}/usr/bin/pto2mk"
  rm -v "${pkgdir}/usr/bin/linefind"
  rm -v "${pkgdir}/usr/bin/nona"
  rm -v "${pkgdir}/usr/bin/fulla"
  rm -v "${pkgdir}/usr/bin/align_image_stack"
  rm -v "${pkgdir}/usr/bin/celeste_standalone"
  rm -v "${pkgdir}/usr/bin/vig_optimize"
  rm -v "${pkgdir}/usr/bin/cpfind"
  rm -v "${pkgdir}/usr/bin/tca_correct"
  rm -v "${pkgdir}/usr/bin/pano_trafo"
  rm -v "${pkgdir}/usr/bin/pano_modify"
  rm -v "${pkgdir}/usr/bin/pto_gen"
  rm -v "${pkgdir}/usr/bin/deghosting_mask"
  rm -v "${pkgdir}/usr/bin/pto_merge"
  rm -v "${pkgdir}/usr/bin/checkpto"
  rm -v "${pkgdir}/usr/bin/cpclean"
  rm -v "${pkgdir}/usr/bin/geocpset"
  rm -v "${pkgdir}/usr/bin/pto_lensstack"
  rm -v "${pkgdir}/usr/bin/pto_var"
  rm -v "${pkgdir}/usr/bin/pto_move"
  rm -v "${pkgdir}/usr/bin/pto_template"
 
  msg "Removing hugin-tools libs"
  cd "${pkgdir}/usr/lib/hugin/"
  rm "${pkgdir}/usr/lib/hugin/libhuginbase.so.0.0"
  rm "${pkgdir}/usr/lib/hugin/liblocalfeatures.so.0.0"
  rm "${pkgdir}/usr/lib/hugin/libceleste.so.0.0"
  rm "${pkgdir}/usr/lib/hugin/libmakefilelib.so.0.0"
  rm "${pkgdir}/usr/lib/hugin/libhuginlines.so.0.0"
  rm "${pkgdir}/usr/lib/hugin/libhuginvigraimpex.so.0.0"
}
