pkgname=ibus
pkgver=1.5.16
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux, IBus qt library and IBus qt input method plugin included"
arch=('x86_64')
url="https://github.com/ibus/ibus"
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python3-dbus' 'python3-gobject' 
         'iso-codes' 'python2-gobject2' 'python2-dbus' 'python2-gobject' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'gnome-common' 'gtk-doc' 'qt5-base'
             'unicode-emoji' 'cldr-emoji-annotation')
options=('!emptydirs')
install=ibus.install
source=(https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('54d751585c1d32998a147cb2a31ed9cbda300829')

prepare() {
  sed -i 's|$(libibus) $(libibus_emoji_dialog)|$(libibus_emoji_dialog) $(libibus)|' ibus-$pkgver/ui/gtk3/Makefile.am
}

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --disable-memconf \
    --enable-gtk-doc \
    --enable-ui \
    --enable-wayland \
    --enable-python-library \
    --with-python=python3 
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
