pkgname=firefox-kde
pkgver=60.0
pkgrel=2
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/firefox/"
depends=(gtk3 mozilla-common libxt startup-notification mime-types dbus-glib alsa-lib ffmpeg
         libvpx libevent hunspell libproxy nspr pixman
         kio knotifications kwindowsystem ki18n)
makedepends=(unzip zip diffutils python2 yasm mesa imake libpulse inetutils xorg-server-xvfb
             autoconf2.13 gtk2 gobject-introspection jack rust
             cmake extra-cmake-modules llvm clang)
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'upower: Battery API'
            'firefox-i18n: for multilanguage support')
options=(!emptydirs strip)
screenshot="http://img864.imageshack.us/img864/5116/firefoxm.png"
provides=("firefox=${pkgver}" "kmozillahelper")
conflicts=('kmozillahelper')
replaces=('kmozillahelper')
#
# For whom want to update this package
#
# Notes on mercurial release revision
# http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/SOURCE
# This URL uses CDN so it's faster, but maybe out of date when new firefox release comes out.
#
# Find Firefox KDE related patch:
# patch http://www.rosenauer.org/hg/mozilla/
# patch https://build.opensuse.org/package/show?package=MozillaFirefox&project=mozilla%3AFactory
#
# Usually, we also port non-openSUSE specific patch together.
#
_patchrev=847ae61baab6
_kmozillahelper_commit=2ca28e0 #v5.0.2
_patchurl=http://www.rosenauer.org/hg/mozilla/raw-file/$_patchrev
source=("https://releases.mozilla.org/pub/firefox/releases/${pkgver}/source/${pkgname%%-kde}-${pkgver}.source.tar.xz"
        "git://github.com/openSUSE/kmozillahelper#commit=${_kmozillahelper_commit}"
        'firefox.desktop' 'ddg.xml'
        'pgo-fix-missing-kdejs.patch' 'no-crmf.diff'
        '0001-Bug-1435212-Add-support-for-FFmpeg-4.0.-r-bryce.patch'

        # Firefox patchset
        "$_patchurl/firefox-kde.patch"
        "$_patchurl/firefox-branded-icons.patch"

        # Gecko/toolkit patchset
        "$_patchurl/mozilla-nongnome-proxies.patch"
        "$_patchurl/mozilla-kde.patch"
        "$_patchurl/mozilla-enable-csd.patch"
        
        # Gentoo patch
        1004_fix_pie_detection.patch
        1005_fix_fortify_sources.patch
        2000_system_harfbuzz.patch
        2001_system_graphite2.patch
        2003_nICER_implicit_decls.patch
        2004_fix_lto_builds.patch

        # Useless patchset
        #"$_patchurl/gecko-lockdown.patch"
        #"$_patchurl/mozilla-idldir.patch"
        #$_patchurl/firefox-ui-lockdown.patch
        #"$_patchurl/toolkit-ui-lockdown.patch"
        #"$_patchurl/mozilla-bmo1088588.patch" # Specify for ARM and PPC
        #"$_patchurl/toolkit-download-folder.patch"
        #"$_patchurl/mozilla-language.patch" # making use of LANGUAGE= env var which isn't part of our standard
        #"$_patchurl/mozilla-prefer_plugin_pref.patch" removed from opensuse. reason: dropped mozilla-prefer_plugin_pref.patch as this feature is likely not worth maintaining further
        #"$_patchurl/mozilla-shared-nss-db.patch" shared by default now
)
sha256sums=('4bc4fd997e6239f53e16a70b04f197791e5c5d09367e2916bcfe4a427999f842'
            'SKIP'
            '6c2ea431c36afef67710547ae67e81975254309e9e1c82d8312f765caa950fe5'
            '1190f251471305f4c96a313b32aa66ee1549125300d7ce385356f45bd8ebcb3d'
            'c6cff32939199189ca89a49860db0f71b5c445b9114609e8d344f5f7d8f68a2d'
            '02000d185e647aa20ca336e595b4004bb29cdae9d8f317f90078bdcc7a36e873'
            '81ed8187ad0bc7d751d0bc4b48f57c3855905ca21a060390383c9c9f98b55dba'
            '6b73aaa38847c320ff26f90118d07433049f63341b64359acf11b1e4f77d50b9'
            '67ae8fc341a465c1e0961014bed5ad89a66a211362c82451c6e068b0e4ad2eb8'
            'ef0f90c9134ef05b950f06a3ffbd699c2e5a5f99a4cdf9868e799534d68c204f'
            '2471b962a512addebf37778c80c9445354e702b63be3636a9e124aa2fd6b743b'
            'ca1602c69f22ee1bd1000cebb883895eeb19b0e1647dfe4ca00422f39ed215aa'
            '2b813ba66a3904b3e745d199479967dafc9b23e69db2238a932e013cdc21b91d'
            'e5d8f0410ecd05119119e2d44a88fb5f1eb67a5b1035fd0a66b8cf167fb81c5d'
            'a850e98166d89f6541f04ac188c4bbdf075f2be601d3d6e88b17d20048ae5243'
            '6d9a7d3059d9017c1d3c28b38f257fe99b48ebcb1d829d427db59f80826fdc89'
            'c39d22c4223c99181e1e828c1d6ba3bde035f497744ad423c60da91425ac4942'
            'aee8725b94b19a6a08c3496c3844d174760d608e9c97f2daae05c7af28f290a4')


# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_google_api_key=AIzaSyDjyg8EmaRUIsewzdjZXFZ0O8N5ARDUDGU

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_mozilla_api_key=bf05f841-e0bd-4644-81f5-3c132755f2e9


prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python

  #cd ${srcdir}/mozilla-release-${_changeset}
  cd ${srcdir}/${pkgname%%-kde}-${pkgver}

  # https://bugzilla.mozilla.org/show_bug.cgi?id=1371991
  patch -Np1 -i ../no-crmf.diff

  echo -n "$_google_api_key" > google-api-key
  echo -n "$_mozilla_api_key" > mozilla-api-key

  msg 'Generating kde.js'
  cat >kde.js <<END
pref("browser.preferences.instantApply", false);
pref("browser.backspace_action", 0);
END

  msg 'Generating mozconfig'
# There multiple default mozconfigs in the soruce tree with references across directories
# $topsrcdir/build/unix/mozconfig.gtk
# $topsrcdir/build/unix/mozconfig.linux
# $topsrcdir/build/mozconfig.common
# $topsrcdir/browser/config/mozconfigs/linux64/release
# $topsrcdir/browser/config/mozconfigs/linux64/common-opt
# $topsrcdir/browser/config/mozconfig
# $topsrcdir/build/mozconfig.rust
# But we intend to mantain our own mozconfig without reference to any of the mozilla upstream configuration to keep it simple
# This mozconfig would inlcude most of the configurations listed above into one file

  cat >.mozconfig <<END
ac_add_options --prefix=/usr
ac_add_options --enable-release
ac_add_options --enable-pie
ac_add_options --enable-gold
ac_add_options --enable-optimize="-O2"
ac_add_options --enable-rust-simd
ac_add_options MOZ_PGO=1

# Release branding
ac_add_options --enable-application=browser
ac_add_options --enable-official-branding
ac_add_options --enable-update-channel=release
ac_add_options --with-distribution-id=org.chakralinux
ac_add_options --enable-default-toolkit=cairo-gtk3
export MOZILLA_OFFICIAL=1
export MOZ_TELEMETRY_REPORTING=1
export MOZ_ADDON_SIGNING=1
export MOZ_REQUIRE_SIGNING=1

# Keys
ac_add_options --with-google-api-keyfile=${PWD@Q}/google-api-key
ac_add_options --with-mozilla-api-keyfile=${PWD@Q}/mozilla-api-key

# System libraries
ac_add_options --with-system-jpeg
ac_add_options --with-system-zlib
ac_add_options --with-system-bz2
ac_add_options --with-system-libevent
ac_add_options --with-system-libvpx
ac_add_options --with-system-nspr
ac_add_options --with-system-icu
ac_add_options --with-system-png
ac_add_options --enable-system-pixman
ac_add_options --enable-system-hunspell
ac_add_options --enable-system-ffi
ac_add_options --enable-libproxy

# Features
ac_add_options --enable-startup-notification
ac_add_options --enable-jack
ac_add_options --enable-alsa
ac_add_options --enable-gconf
ac_add_options --disable-updater
ac_add_options --enable-crashreporter

# PGO
mk_add_options PROFILE_GEN_SCRIPT='EXTRA_TEST_ARGS=10 \$(MAKE) -C \$(MOZ_OBJDIR) pgo-profile-run'
mk_add_options MOZ_OBJDIR=@TOPSRCDIR@/obj-ff
mk_add_options MOZ_MAKE_FLAGS="-j9"
STRIP_FLAGS="--strip-debug"
END

  msg "Gecko/toolkit patchset"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch" 
  patch -Np1 -i "$srcdir/mozilla-enable-csd.patch"

  msg "Firefox patchset"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-branded-icons.patch"
  patch -Np0 -i "$srcdir/pgo-fix-missing-kdejs.patch"

  msg "Gentoo Patchset"
  patch -Np1 -i $srcdir/1004_fix_pie_detection.patch
  patch -Np1 -i $srcdir/1005_fix_fortify_sources.patch
  patch -Np1 -i $srcdir/2000_system_harfbuzz.patch
  patch -Np1 -i $srcdir/2001_system_graphite2.patch
  patch -Np1 -i $srcdir/2003_nICER_implicit_decls.patch
  patch -Np1 -i $srcdir/2004_fix_lto_builds.patch

  msg "Arch Patchset"
  patch -Np1 -i "$srcdir/0001-Bug-1435212-Add-support-for-FFmpeg-4.0.-r-bryce.patch"

  sed -i \
    -e '/^OS_LIBS += no_as_needed/d' \
    -e '/^OS_LIBS += as_needed/d' \
    ./widget/gtk/mozgtk/gtk2/moz.build \
    ./widget/gtk/mozgtk/gtk3/moz.build

  SHELL=/bin/bash MOZ_NOSPAM=1 ./mach configure
}

build() {
  msg "build_kmozillahelper"
  build_kmozillahelper
  msg "build_firefox"
  build_firefox
}

build_kmozillahelper() {
  cd $srcdir/
  mkdir -p kmozillahelper-build
  cd kmozillahelper-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE=Release ../kmozillahelper
  make
}

build_firefox() {
  cd $srcdir/${pkgname%%-kde}-${pkgver}

  # _FORTIFY_SOURCE causes configure failures
  CPPFLAGS+=" -O2"

  export PATH="$srcdir/path:$PATH"
  export SHELL=/bin/bash

  # Do PGO
  xvfb-run -a -n 95 -s "-extension GLX -screen 0 1280x1024x24" \
       ./mach build --verbose
  ./mach buildsymbols
  #make -f client.mk build
}

package() {
  msg "package() kmozillahelper"
  cd $srcdir/kmozillahelper-build
  make DESTDIR=$pkgdir install

  msg "package() Firefox"
  #cd "$srcdir/mozilla-release-${_changeset}"
  cd $srcdir/${pkgname%%-kde}-${pkgver}
 
  export SHELL=/bin/bash
  DESTDIR="$pkgdir" ./mach install
  find . -name '*crashreporter-symbols-full.zip' -exec cp -fvt "$startdir" {} +

  _vendorjs="$pkgdir/usr/lib/firefox/browser/defaults/preferences/vendor.js"
  install -Dm644 /dev/stdin "$_vendorjs" <<END
// Use LANG environment variable to choose locale
pref("intl.locale.requested", "");

// Disable default browser checking.
pref("browser.shell.checkDefaultBrowser", false);

// Don't disable our bundled extensions in the application directory
pref("extensions.autoDisableScopes", 11);
pref("extensions.shownSelectionUI", true);
END

  install -Dm644 kde.js "$pkgdir/usr/lib/firefox/browser/defaults/preferences/kde.js"

  msg 'Chakra branding'
  _distini="$pkgdir/usr/lib/firefox/distribution/distribution.ini"
  install -Dm644 /dev/stdin "$_distini" <<END
[Global]
id=chakralinux
version=1.0
about=Mozilla Firefox compiled by Chakra GNU/Linux

[Preferences]
app.distributor=chakralinux
app.distributor.channel=firefox
app.partner.chakralinux=chakralinux
END
  install -Dm644 $srcdir/ddg.xml $pkgdir/usr/lib/firefox/distribution/searchplugins/common/ddg-chakra.xml
  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 browser/branding/official/default$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/firefox.png"

  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -r "$pkgdir"/usr/lib/firefox/dictionaries
  ln -Ts /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -Ts /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # Install a wrapper to avoid confusion about binary path
  install -Dm755 /dev/stdin "$pkgdir/usr/bin/firefox" <<END
#!/bin/sh
exec /usr/lib/firefox/firefox "\$@"
END

  # Replace duplicate binary with wrapper
  # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -srf "$pkgdir/usr/bin/firefox" \
    "$pkgdir/usr/lib/firefox/firefox-bin"
}
