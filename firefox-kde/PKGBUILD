# Contributor: Weng Xuetian <wengxt@gmail.com>

pkgname=firefox-kde
pkgver=28.0
pkgrel=2
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
_kmozillahelper_obsver=b88c72dfdc858f6209feb123227bd7df
_kmozillahelper_pkgname=kmozillahelper
_kmozillahelper_pkgver=0.6.4
url='http://www.mozilla.org/projects/firefox'
arch=('x86_64')
license=('MPL')
depends=('gtk2' 'startup-notification' 'libnotify' 'alsa-lib' 'gstreamer0.10' 'gstreamer0.10-base' 'libxt' 'dbus-glib' 'libevent' 'sqlite3' 'kdelibs' 'nss')
makedepends=('zip' 'pkg-config' 'diffutils' 'python2' 'wireless_tools' 'unzip' 'autoconf2.13' 'mesa' 'yasm' 'cmake' 'automoc4' 'libpulse')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libpulse: PulseAudio audio driver')
screenshot="http://img864.imageshack.us/img864/5116/firefoxm.png"
provides=("firefox=${pkgver}" "kmozillahelper=${_kmozillahelper_pkgver}")
conflicts=('firefox' 'kmozillahelper')
replaces=('kmozillahelper')
install=firefox.install
url="http://www.mozilla.org/projects/firefox"
optdepends=('firefox-i18n: for multilanguage support')
#
# For whom want to update this package
#
# Alternative firefox source url: 
# http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
# This URL uses CDN so it's faster, but maybe out of date when new firefox release comes out.
#
# Find Firefox KDE related patch:
# patch http://www.rosenauer.org/hg/mozilla/
# patch https://build.opensuse.org/package/show?package=MozillaFirefox&project=mozilla%3AFactory
#
# Usually, we also port non-openSUSE specific patch together.
#
_patchrev=427ae22e730e
_patchurl=http://www.rosenauer.org/hg/mozilla/raw-file/$_patchrev
source=(ftp://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
        mozconfig
        firefox.desktop
        firefox-install-dir.patch
        vendor.js
        kde.js
	firefox-20.0.1-fixed-loading-icon.png
	duckduckgo.xml
	"kmozillahelper-0.6.4.tar.bz2::https://api.opensuse.org/public/source/mozilla:Factory/mozilla-kde4-integration/${_kmozillahelper_pkgname}-${_kmozillahelper_pkgver}.tar.bz2?rev=${_kmozillahelper_obsver}"

	# Firefox patchset
	$_patchurl/firefox-browser-css.patch
	#$_patchurl/firefox-kde.patch
	# manually removed package-manifest.in in firefox-kde.patch to be able to build with PDO
	firefox-kde.patch
	$_patchurl/firefox-kde-114.patch
	$_patchurl/firefox-no-default-ualocale.patch
	$_patchurl/firefox-branded-icons.patch
	# Gecko/toolkit patchset
	$_patchurl/toolkit-download-folder.patch
	$_patchurl/mozilla-nongnome-proxies.patch
	$_patchurl/mozilla-prefer_plugin_pref.patch
	$_patchurl/mozilla-kde.patch
	$_patchurl/mozilla-preferences.patch
	$_patchurl/mozilla-language.patch
)

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys.
_google_api_key=AIzaSyA33sdKPff0x3KBByOhpzc1i7RS2A1mEpY

prepare() {
  cd "mozilla-release"

  cp "$srcdir/mozconfig" .mozconfig
  
  patch -Np1 -i "$srcdir/firefox-install-dir.patch"
  echo -n "$_google_api_key" > google-api-key

  # make makepkg -R easier
  msg "Patching for KDE"
  rm -f content/media/gstreamer/nsGStreamerFormatHelper.{h,cpp}
  rm -f browser/components/shell/src/{nsKDEShellService,nsUnixShellService}.{h,cpp}
  rm -f browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.{h,cpp}
  rm -f uriloader/exthandler/unix/{nsCommonRegistry,nsKDERegistry}.{h,cpp}
  rm -f toolkit/content/widgets/{dialog,preferences}-kde.xml

  msg "Gecko/toolkit patchset"
  #patch -Np1 -i "$srcdir/toolkit-download-folder.patch"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-prefer_plugin_pref.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  #patch -Np1 -i "$srcdir/mozilla-preferences.patch" #At startup, read and use the OSâ€™s locale. 
  patch -Np1 -i "$srcdir/mozilla-language.patch"

  msg "Firefox patchset"
  patch -Np1 -i "$srcdir/firefox-browser-css.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-kde-114.patch"
  patch -Np1 -i "$srcdir/firefox-no-default-ualocale.patch"
  patch -Np1 -i "$srcdir/firefox-branded-icons.patch"
  

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  # WebRTC build tries to execute "python" and expects Python 2
  mkdir -p "$srcdir/path"
  ln -sf /usr/bin/python2 "$srcdir/path/python"

  # Fix tab loading icon (flickers with libpng 1.6)
  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
  cp "$srcdir/firefox-20.0.1-fixed-loading-icon.png" \
    browser/themes/linux/tabbrowser/loading.png 

}

build() {
  cd  "$srcdir/mozilla-release"

  msg "build_kmozillahelper"
  build_kmozillahelper
  msg "build_firefox"
  build_firefox
}

build_kmozillahelper() {
  cd $srcdir/
  mkdir -p kmozillahelper-build
  cd kmozillahelper-build

  cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` -DCMAKE_BUILD_TYPE=Release ../kmozillahelper
  make
}

build_firefox() {
  cd  "$srcdir/mozilla-release"
 
  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS
  
  # Enabled PGO: https://developer.mozilla.org/en-US/docs/Building_with_Profile-Guided_Optimization
  #make -f client.mk profiledbuild MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  
  # Set up PGO
  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &
  
  # Build
  if ! make -f client.mk build MOZ_PGO=1; then
    kill $!
    return 1
  fi

  # Kill leftovers
  kill $! || true

}

package() {
  msg "package() kmozillahelper"
  cd $srcdir/kmozillahelper-build
  make DESTDIR=$pkgdir install

  msg "package() Firefox"
  cd "$srcdir/mozilla-release"
  cp "$srcdir/kde.js" obj-ff/dist/bin/defaults/pref
  
  make -f client.mk DESTDIR="$pkgdir" install

  install -Dm644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/preferences/vendor.js"
  install -Dm644 ../kde.js "$pkgdir/usr/lib/firefox/defaults/preferences/kde.js"
  install -Dm644 ../duckduckgo.xml "$pkgdir/usr/lib/firefox/browser/searchplugins/duckduckgo.xml"

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done

  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"
    
  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"
  
  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}
  
  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
  
}

sha1sums=('f78517836ceca0cd2a0d3db1f282985c616e3fae'
          '311b020505e3080eb62760a520e23dda1d97e926'
          '5c54c004a5f6bcf22d1ff0fdc33765c45517288a'
          'db2d93a36184cc8e2edd4149052da382b5b43bfd'
          'e0f399de01da908f474c237bc6c5a15678cd8fcd'
          'e13d5c19b3d9781ed67acc6c55c18219b0249736'
          '0c3900ffe21bfc3d96b80ade334613bc7e76fe27'
          'aa3442d291bd77fefa3758291baa9c609a74e5dc'
          '5543ea72a07fb6f3af19cfbb6d1683a5fddaa807'
          '5400573a902cb46f478a5f3577ef8f86790f69e8'
          '1f7affd939c265c409b34154250b07f94947bbf0'
          '84e6e4eb614e86baf1ee565da9c2ac505940492a'
          'b911531a18bcfc686267d8e5fdb7cb37769f7c42'
          '02633df9bcaec095258f8ae02688c7ac96d8ecb7'
          '2dfca8f76045c089941f3defc89d5f1171efa552'
          '5a38d441d73647b12e1e13416de3fde822dfed18'
          '05072b88a7707abca65f84cc6d1c4985c6874f6f'
          '9b6075843127a1f2a33cfe2c758112bcda8790a0'
          '18a6e8bf4e173721f3f7d22da2636fc6d0a40b71'
          '136fa1ae86e155aa31e087ee1923504aeb61ef86')
