pkgname=firefox-kde
pkgver=52.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/firefox/"
depends=(gtk3 mozilla-common libxt startup-notification mime-types dbus-glib alsa-lib ffmpeg
         libvpx libevent nss hunspell
         "sqlite3>=3.17.0" # icu>=58.1
         kio knotifications kwindowsystem ki18n)
makedepends=(unzip zip diffutils python2 yasm mesa imake libpulse inetutils xorg-server-xvfb
             autoconf2.13 cargo gtk2 gobject-introspection
             cmake extra-cmake-modules)
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'upower: Battery API'
            'firefox-i18n: for multilanguage support')
options=(!emptydirs !makeflags)
screenshot="http://img864.imageshack.us/img864/5116/firefoxm.png"
provides=("firefox=${pkgver}" "kmozillahelper")
conflicts=('kmozillahelper')
replaces=('kmozillahelper')
install=firefox.install
#
# For whom want to update this package
#
# Alternative firefox source url: 
# http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
# This URL uses CDN so it's faster, but maybe out of date when new firefox release comes out.
#
# Find Firefox KDE related patch:
# patch http://www.rosenauer.org/hg/mozilla/
# patch https://build.opensuse.org/package/show?package=MozillaFirefox&project=mozilla%3AFactory
#
# Usually, we also port non-openSUSE specific patch together.
#
_patchrev=34bd1eb1cbd7
_kmozillahelper_commit=21af64b
_patchurl=http://www.rosenauer.org/hg/mozilla/raw-file/$_patchrev
source=("https://download-installer.cdn.mozilla.net/pub/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.xz"
        "git://github.com/openSUSE/kmozillahelper#commit=${_kmozillahelper_commit}"
        'mozconfig'
        'firefox.desktop'
        'firefox-install-dir.patch'
        'vendor.js'
        'kde.js'
        'pgo-fix-missing-kdejs.patch'
	'fix-wifi-scanner.diff'

        # Firefox patchset
        "$_patchurl/firefox-kde.patch"
        "$_patchurl/firefox-branded-icons.patch"
        "$_patchurl/firefox-no-default-ualocale.patch" # try to refer to chrome://global/locale/intl.properties(which is defined in language pack) for ua locale

        # Gecko/toolkit patchset
        "$_patchurl/mozilla-nongnome-proxies.patch"
        "$_patchurl/mozilla-kde.patch"
	'ddg-branding.patch'
        "$_patchurl/mozilla-openaes-decl.patch"
        "$_patchurl/mozilla-preferences.patch"  # try to check OS's locale and use it first rather than stick to general.useragent.locale
        "$_patchurl/mozilla-shared-nss-db.patch"

        # Useless patchset
        #"$_patchurl/gecko-lockdown.patch"
        #"$_patchurl/mozilla-idldir.patch"
        #$_patchurl/firefox-ui-lockdown.patch
        #"$_patchurl/toolkit-ui-lockdown.patch"
        #"$_patchurl/mozilla-bmo1088588.patch" # Specify for ARM and PPC
        #"$_patchurl/toolkit-download-folder.patch"
        #"$_patchurl/mozilla-language.patch" # making use of LANGUAGE= env var which isn't part of our standard
        #"$_patchurl/mozilla-prefer_plugin_pref.patch" removed from opensuse. reason: dropped mozilla-prefer_plugin_pref.patch as this feature is likely not worth maintaining further
)
sha1sums=('1d0871889fd57a51ffa38c3ec9e7a2125b38e6bf'
          'SKIP'
          'c2d4e8c39bc1b33339eb2778b59dac9c90e7ec03'
          '1a8ecc6d2cfd97791d8182882a74929f2a7289e9'
          '693b54b9206ad854e4d75cbbf393029b77e82153'
          '05b3a893e422ff579663c351f275f71010fa9dca'
          'ce48f8e5065319e191a7389539ec663dc8a919d7'
          'e480985ad1c4277e48a7d69c07258f0d7e97a977'
          '9ed341bb7b147da7ebba420110dcf5a5a8e8d887'
          '28b26ceb95fdea9939940a5c5c7e7fc5dca9ea1b'
          '255d7465aa5ace5efb4f4cde14fa631872447aa3'
          'dbfd269cbf88a4e1faea99bffc57ba26b8a0a7d9'
          '5a38d441d73647b12e1e13416de3fde822dfed18'
          '5defe0a718fb2cf631441464049073bd2a5e6697'
          '9cf8c5a03bcc97b66d97d62d72c325ddb14102cb'
          '5978fb0131d31b0e91459a90edc918f66eec3155'
          'bcf0f111eadcf4e7d3316659af2f210c583ad9ff'
          'efd2e4fab5ea174f5f84db17265cd1d070e414c8')


# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_google_api_key=AIzaSyDjyg8EmaRUIsewzdjZXFZ0O8N5ARDUDGU

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_mozilla_api_key=bf05f841-e0bd-4644-81f5-3c132755f2e9


prepare() {
  cd ${srcdir}/firefox-${pkgver}/

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch

  # https://bugzilla.mozilla.org/show_bug.cgi?id=1314968
  patch -Np1 -i ../fix-wifi-scanner.diff

  echo -n "$_google_api_key" > google-api-key
  echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

  echo -n "$_mozilla_api_key" > mozilla-api-key
  echo "ac_add_options --with-mozilla-api-keyfile=\"$PWD/mozilla-api-key\"" >>.mozconfig


  # make makepkg -R easier
  msg "Patching for KDE"
  rm -f content/media/gstreamer/nsGStreamerFormatHelper.{h,cpp}
  rm -f browser/components/shell/{nsKDEShellService,nsUnixShellService}.{h,cpp}
  rm -f browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.{h,cpp}
  rm -f uriloader/exthandler/unix/{nsCommonRegistry,nsKDERegistry}.{h,cpp}
  rm -f toolkit/content/widgets/{dialog,preferences}-kde.xml

  msg "Gecko/toolkit patchset"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch" 
  patch -Np1 -i "$srcdir/mozilla-preferences.patch" 
  patch -Np1 -i "$srcdir/mozilla-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/mozilla-openaes-decl.patch"

  msg "Firefox patchset"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-branded-icons.patch"
  patch -Np1 -i "$srcdir/ddg-branding.patch"
  patch -Np1 -i "$srcdir"/pgo-fix-missing-kdejs.patch
  patch -Np1 -i "$srcdir/firefox-no-default-ualocale.patch"

  mkdir -vp "$srcdir/path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -sf /usr/bin/python2 "$srcdir/path/python"

  # fix GTK3
  # sed -i 's|parent->group|gtk_window_get_group(const_cast<GtkWindow*>(parent))|g' toolkit/xre/nsKDEUtils.cpp 
}

build() {
  msg "build_kmozillahelper"
  build_kmozillahelper
  msg "build_firefox"
  build_firefox
}

build_kmozillahelper() {
  cd $srcdir/
  mkdir -p kmozillahelper-build
  cd kmozillahelper-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../kmozillahelper
  make
}

build_firefox() {
  cd  "$srcdir/firefox-${pkgver}"

  # _FORTIFY_SOURCE causes configure failures
  CPPFLAGS+=" -O2"

  # Hardening
  LDFLAGS+=" -Wl,-z,now"

  # GCC 6
  # CXXFLAGS+=" -fno-delete-null-pointer-checks -fno-lifetime-dse -fno-schedule-insns2"

  export PATH="$srcdir/path:$PATH"
  export PYTHON="/usr/bin/python2"
  export SHELL=/bin/bash

  # Do PGO
  #xvfb-run -a -n 95 -s "-extension GLX -screen 0 1280x1024x24" \
  #  make -f client.mk build MOZ_PGO=1
  make -f client.mk build
}

package() {
  msg "package() kmozillahelper"
  cd $srcdir/kmozillahelper-build
  make DESTDIR=$pkgdir install

  msg "package() Firefox"
  cd "$srcdir/firefox-${pkgver}"
 
  export SHELL=/bin/bash
  make -f client.mk DESTDIR="$pkgdir" MOZ_PKG_FATAL_WARNINGS=0 install 

  install -Dm644 ../vendor.js "$pkgdir/usr/lib/firefox/browser/defaults/preferences/vendor.js"
  install -Dm644 ../kde.js "$pkgdir/usr/lib/firefox/browser/defaults/preferences/kde.js"

  for i in 16 22 24 32 48 256; do
    install -Dm644 browser/branding/official/default$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -Dm644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -Dm644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/firefox.png"

  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}
  # Use system-provided dictionaries
  rm -r "$pkgdir"/usr/lib/firefox/dictionaries
  ln -Ts /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -Ts /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # Install a wrapper to avoid confusion about binary path
  install -Dm755 /dev/stdin "$pkgdir/usr/bin/firefox" <<END
#!/bin/sh
exec /usr/lib/firefox/firefox "\$@"
END

  # Replace duplicate binary with wrapper
  # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -srf "$pkgdir/usr/bin/firefox" \
    "$pkgdir/usr/lib/firefox/firefox-bin"
}
