pkgname=firefox-kde
pkgver=43.0
pkgrel=5
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
_kmozillahelper_commit=83cd08e0c917fda16b30d91f929779304c46d273
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/firefox/"
depends=('gtk3' 'gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'icu' 'libevent' 'nss>=3.19.4' 'hunspell' 'sqlite3>=3.8.9'
         'kio' 'knotifications' 'nspr>=4.10.10' 'libffi>=3.0.9')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake'
             'xorg-server-xvfb' 'libpulse' 'ffmpeg'
             'inetutils' 'libpng>=1.6.16'
             'cmake' 'extra-cmake-modules' 'clang')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'firefox-i18n: for multilanguage support')
screenshot="http://img864.imageshack.us/img864/5116/firefoxm.png"
provides=("firefox=${pkgver}" "kmozillahelper")
conflicts=('kmozillahelper')
replaces=('kmozillahelper')
install=firefox.install
#
# For whom want to update this package
#
# Alternative firefox source url: 
# http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
# This URL uses CDN so it's faster, but maybe out of date when new firefox release comes out.
#
# Find Firefox KDE related patch:
# patch http://www.rosenauer.org/hg/mozilla/
# patch https://build.opensuse.org/package/show?package=MozillaFirefox&project=mozilla%3AFactory
#
# Usually, we also port non-openSUSE specific patch together.
#
_patchrev=de3a92aed259
_patchurl=http://www.rosenauer.org/hg/mozilla/raw-file/$_patchrev
source=("https://download-installer.cdn.mozilla.net/pub/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.xz"
        "git://github.com/openSUSE/kmozillahelper#commit=${_kmozillahelper_commit}"
        'mozconfig'
        'firefox.desktop'
        'vendor.js'
        'kde.js'
        'firefox-20.0.1-fixed-loading-icon.png'
        # The KMozillaHelper unofficial Kf5 Port

        'firefox-install-dir.patch'
        'rhbz-966424.patch'
        'pgo-fix-missing-kdejs.patch'

        # Firefox patchset
        "$_patchurl/firefox-kde.patch"
        "$_patchurl/firefox-branded-icons.patch"

        # Gecko/toolkit patchset
        "$_patchurl/mozilla-nongnome-proxies.patch"
        # "$_patchurl/mozilla-prefer_plugin_pref.patch" removed from opensuse. reason: dropped mozilla-prefer_plugin_pref.patch as this feature is likely not worth maintaining further
        "$_patchurl/mozilla-kde.patch"
	"$_patchurl/mozilla-preferences.patch"
        "$_patchurl/mozilla-pkgconfig.patch"
        #'install-kde-js.patch'
        'ddg-branding.patch'


	# GTK3 patchset
#	'firefox-build.patch'
#	'mozilla-1192243.patch'
#	'xulrunner-24.0-jemalloc-ppc.patch'
#	'xulrunner-24.0-s390-inlines.patch'
	# FIXME: This is not working! THANKS! FEDORA!


        # Useless patchset
        #"$_patchurl/gecko-lockdown.patch"
        #"$_patchurl/mozilla-idldir.patch"
        #$_patchurl/firefox-ui-lockdown.patch
        #"$_patchurl/toolkit-ui-lockdown.patch"
        #"$_patchurl/mozilla-shared-nss-db.patch"
        #"$_patchurl/mozilla-bmo1088588.patch" # Specify for ARM and PPC
        #"$_patchurl/toolkit-download-folder.patch"
        #"$_patchurl/mozilla-language.patch"
        #"$_patchurl/firefox-no-default-ualocale.patch"
)
sha1sums=('65913f9dd61b49b341a66a514c9cac23b602c7c8'
          'SKIP'
          '2dc77dc4096c69e178ac79ea86f8cf184dc40932'
          '86d6c2fe4ec2b3072a132e2afbc0ac798fd51e8f'
          'ca77a08b296f8673828898d91b20cc6dd8742c2f'
          'ce48f8e5065319e191a7389539ec663dc8a919d7'
          '0c3900ffe21bfc3d96b80ade334613bc7e76fe27'
          'a87a6eb5cb86f63e39249c60eb53594ce225dea0'
          'ad1f05abf477dbc5f7e72c06e37c180459eeffd4'
          '6991d61be593b5ad109174ebc7d60763eb48125b'
          '6c026263dd9d4dfc2f5bbd0b5fafb176acabeee4'
          'b7fa502cf654f9b2d12e89e5a17ae4af90090cc6'
          '5a38d441d73647b12e1e13416de3fde822dfed18'
          '6e5badf838c7483a2dd3b58eb5dccf043c36b2ec'
          'bcf0f111eadcf4e7d3316659af2f210c583ad9ff'
          'ebd57f47a4a8a156afc8ad32eeceac78162da548'
          'a169730cda326effbf54e59e9adb9d595f36be41')


# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys.
_google_api_key=AIzaSyA33sdKPff0x3KBByOhpzc1i7RS2A1mEpY

prepare() {
  cd ${srcdir}/firefox-${pkgver}/

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch

  # https://bugs.archlinux.org/task/41689
  patch -Np1 -i ../rhbz-966424.patch

  echo -n "$_google_api_key" > google-api-key
  echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

  # make makepkg -R easier
  msg "Patching for KDE"
  rm -f content/media/gstreamer/nsGStreamerFormatHelper.{h,cpp}
  rm -f browser/components/shell/{nsKDEShellService,nsUnixShellService}.{h,cpp}
  rm -f browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.{h,cpp}
  rm -f uriloader/exthandler/unix/{nsCommonRegistry,nsKDERegistry}.{h,cpp}
  rm -f toolkit/content/widgets/{dialog,preferences}-kde.xml

  msg "Gecko/toolkit patchset"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"

  msg "Firefox patchset"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-branded-icons.patch"
  patch -Np1 -i "$srcdir/ddg-branding.patch"
  patch -Np1 -i "$srcdir"/pgo-fix-missing-kdejs.patch

  # fix GTK3
  sed -i 's|parent->group|gtk_window_get_group(const_cast<GtkWindow*>(parent))|g' toolkit/xre/nsKDEUtils.cpp 

#  msg "GTK3 patchset"
#  patch -p2 -i ../firefox-build.patch
#  patch -p2 -i ../xulrunner-24.0-jemalloc-ppc.patch
#  patch -p2 -i ../xulrunner-24.0-s390-inlines.patch
#  patch -p1 -i ../mozilla-1192243.patch

  mkdir -vp "$srcdir/path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -sf /usr/bin/python2 "$srcdir/path/python"

  # Use gold, as Mozilla can use some of its features, such as safe ICF
  #ln -s /usr/bin/ld.gold "$srcdir/path/ld"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  # Fix tab loading icon (doesn't work with libpng 1.6)
  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
  cp "$srcdir/firefox-20.0.1-fixed-loading-icon.png" \
    browser/themes/linux/tabbrowser/loading.png
}

build() {
  msg "build_kmozillahelper"
  build_kmozillahelper
  msg "build_firefox"
  build_firefox
}

build_kmozillahelper() {
  cd $srcdir/
  mkdir -p kmozillahelper-build
  cd kmozillahelper-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../kmozillahelper
  make
}

build_firefox() {
  cd  "$srcdir/firefox-${pkgver}"
 
  export PATH="$srcdir/path:$PATH"
  export PYTHON="/usr/bin/python2"
  export SHELL=/bin/bash

  # Do PGO
  xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
    make -f client.mk build MOZ_PGO=1
}

package() {
  msg "package() kmozillahelper"
  cd $srcdir/kmozillahelper-build
  make DESTDIR=$pkgdir install

  msg "package() Firefox"
  cd "$srcdir/firefox-${pkgver}"
 
  export SHELL=/bin/bash
  make -f client.mk DESTDIR="$pkgdir" MOZ_PKG_FATAL_WARNINGS=0 install 

  install -Dm644 ../vendor.js "$pkgdir/usr/lib/firefox/browser/defaults/preferences/vendor.js"
  install -Dm644 ../kde.js "$pkgdir/usr/lib/firefox/browser/defaults/preferences/kde.js"

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -Dm644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -Dm644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/firefox.png"

  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
