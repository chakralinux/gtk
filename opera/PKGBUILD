# Maintainer: AlmAck

pkgname=opera
pkgver=28.0.1750.48
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite."
url="http://www.opera.com/"
install=${pkgname}.install
options=(!strip !zipman)
license=('custom:opera')
backup=("etc/$pkgname/default")
arch=('x86_64')
depends=('gtk2' 'desktop-file-utils' 'shared-mime-info' 'libxtst' 'libxss' 'gcc-libs' 'alsa-lib' 'nss' 'freetype2' 'gconf')
optdepends=('pepperflashplugin: Pepper Flash plugin'
            'curl: opera crash reporter and autoupdate checker'
            'ffmpeg: playback of not really open formats')
source=("http://get.opera.com/pub/${pkgname}/desktop/${pkgver}/linux/${pkgname}-stable_${pkgver}_amd64.deb"
	'opera'
	'default')
md5sums=('bf3cfcd6a3d8c824eb26ed89dce5d9e5'
         '924d6178b38638303db9fde37c6055fd'
         '50c4f4ec845ad8629ec3f308ab8cd991')

prepare() {
  sed -e "s/%pkgname%/$pkgname/g" -i "$srcdir/opera"
  sed -e "s/%operabin%/x86_64-linux-gnu\/$pkgname\/$pkgname/g" \
          -i "$srcdir/opera"
}

package() {

  tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"

  # soname fix for libsystemd (udev)
  sed -e 's/libudev.so.0/libudev.so.1/g' \
      -i "$pkgdir/usr/lib/x86_64-linux-gnu/$pkgname/$pkgname"

  # suid opera_sandbox
  chmod 4755 "$pkgdir/usr/lib/x86_64-linux-gnu/$pkgname/opera_sandbox"

  # install default options
  install -Dm644 "$srcdir/default" "$pkgdir/etc/$pkgname/default"

  # install opera wrapper
  rm "$pkgdir/usr/bin/$pkgname"
  install -Dm755 "$srcdir/opera" "$pkgdir/usr/bin/$pkgname"

  # license
  install -Dm644 \
          "$pkgdir/usr/lib/x86_64-linux-gnu/$pkgname/opera_autoupdate.licenses" \
          "$pkgdir/usr/share/licenses/$pkgname/opera_autoupdate.licenses"

  install -Dm644 \
          "$pkgdir/usr/share/doc/${pkgname}-stable/copyright" \
          "$pkgdir/usr/share/licenses/$pkgname/copyright"
}
