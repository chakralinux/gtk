pkgname=opera
pkgver=35.0.2066.37
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite."
url="http://www.opera.com/"
install=${pkgname}.install
options=(!strip !zipman)
license=('custom:opera')
backup=("etc/$pkgname/default")
arch=('x86_64')
depends=('gtk2' 'desktop-file-utils' 'shared-mime-info' 'libxtst' 'libxss' 'gcc-libs' 'alsa-lib' 'nss' 'freetype2' 'gconf' 'libnotify')
optdepends=('pepperflashplugin: Pepper Flash plugin'
            'curl: opera crash reporter and autoupdate checker'
            'opera-ffmpeg-codecs: In some cases, you may need ffmpeg to play streams')
source=("http://get.geo.opera.com/pub/${pkgname}/desktop/${pkgver}/linux/${pkgname}-stable_${pkgver}_amd64.deb"
	'opera'
	'default')
sha256sums=('3afa543f51d5f7fe4e8fbff93518c4afe0a0a8fab5e43210fbc0f6d42739d148'
            '508512464e24126fddfb2c41a1e2e86624bdb0c0748084b6a922573b6cf6b9c5'
            '4913d97dec0ddc99d1e089b029b9123c2c86b7c88d631c4d1111b119b09da027')

prepare() {
    sed -e "s/%pkgname%/$pkgname/g" -i "$srcdir/opera"
    sed -e "s/%operabin%/$pkgname\/$pkgname/g" \
        -i "$srcdir/opera"
}

package() {

  tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"
  
  # get rid of the extra subfolder x86_64-linux-gnu

    (
        cd "$pkgdir/usr/lib/"*-linux-gnu/
        mv "$pkgname" ../
    )
    rm -rf "$pkgdir/usr/lib/"*-linux-gnu

  # soname fix for libsystemd (udev)
  sed -e 's/libudev.so.0/libudev.so.1/g' \
      -i "$pkgdir/usr/lib/$pkgname/$pkgname"

  # suid opera_sandbox
  chmod 4755 "$pkgdir/usr/lib/$pkgname/opera_sandbox"

  # install default options
  install -Dm644 "$srcdir/default" "$pkgdir/etc/$pkgname/default"

  # install opera wrapper
  rm "$pkgdir/usr/bin/$pkgname"
  install -Dm755 "$srcdir/opera" "$pkgdir/usr/bin/$pkgname"

  # license
  install -Dm644 \
          "$pkgdir/usr/lib/$pkgname/opera_autoupdate.licenses" \
          "$pkgdir/usr/share/licenses/$pkgname/opera_autoupdate.licenses"

  install -Dm644 \
          "$pkgdir/usr/share/doc/${pkgname}-stable/copyright" \
          "$pkgdir/usr/share/licenses/$pkgname/copyright"
}
