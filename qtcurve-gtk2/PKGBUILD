pkgbase=qtcurve
pkgname=('qtcurve-gtk2')
pkgver=1.9.1
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('x86_64')
url='https://github.com/QtCurve/qtcurve'
license=('LGPL')
groups=('qtcurve')
depends=('qtcurve-utils' 'gtk2')
makedepends=('extra-cmake-modules' 'cmake' 'qt5-x11extras')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/KDE/qtcurve/archive/$pkgver.tar.gz")
md5sums=('7ecafe114212a75381b460fe3166c5bd')

prepare() {
  cd $srcdir/$pkgbase-$pkgver

  # prepare for build
  [[ -e build ]] && rm -rf build
  mkdir build
}

build() {
  cd $srcdir/$pkgbase-$pkgver/build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT4_ENABLE_KDE=false \
           -DENABLE_QT5=false \
           -DENABLE_GTK2=true
  make
}
 
package_qtcurve-gtk2() {
  cd $srcdir/$pkgbase-$pkgver/build/
  make DESTDIR="$pkgdir" -C gtk2 install
  cd $srcdir/$pkgbase-$pkgver/build/lib/cairo
  make DESTDIR="$pkgdir" install
}
