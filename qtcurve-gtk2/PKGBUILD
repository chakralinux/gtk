pkgbase=qtcurve
pkgname=('qtcurve-gtk2')
pkgver=1.8.17
pkgrel=3
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('x86_64')
url='https://github.com/QtCurve/qtcurve'
license=('GPL')
groups=('qtcurve')
depends=('qtcurve-utils' 'gtk2')
makedepends=('cmake' 'automoc4' 'qt')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
	dep_xcb-util-image_removal.patch)
md5sums=('c4f2e9bc296398b42bfc132b88a3b989'
	 '40bd1d75a948d69df8c6d83f7383300d')

prepare(){
  cd $pkgbase-$pkgver
  
  # fix remove xcb-util-image dependence
  patch -Np1 -i ${srcdir}/dep_xcb-util-image_removal.patch
}

build() {
  cd $pkgbase-$pkgver
 
  mkdir -p build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT4_ENABLE_KDE=false \
           -DENABLE_QT5=false \
           -DENABLE_GTK2=true
  make
}
 
package_qtcurve-gtk2() {
  cd $pkgbase-$pkgver/build/gtk2
  make DESTDIR="$pkgdir" install
}
