# CCR: Ben R <thebenj88 *AT* gmail *DOT* com>

pkgname=libwebkit-gtk2
pkgver=2.4.3
pkgrel=1
pkgdesc="An opensource web content engine"
arch=('x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'icu' 'gst-plugins-base' 'libsoup' 'enchant' 'libwebp')
makedepends=('gperf' 'gobject-introspection' 'python2' 'gtk2' 'ruby')
provides=('libwebkit')
options=('!emptydirs')
install=libwebkit.install
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)
sha256sums=('9862c3eac1d12cc6e52cc39060a1284163950b304d534529a0dbcc5958a8ceac')

build() {

  cd "${srcdir}/webkitgtk-${pkgver}"

  # Suppress a *lot* of gcc 4.8 warnings
  CPPFLAGS+=" -Wno-unused-local-typedefs"

  # fix a bug, where documentation is build even when gtk-doc is not installed, which fails
  # http://www.linuxfromscratch.org/blfs/view/svn/x/webkitgtk.html
  sed -i '/generate-gtkdoc --rebase/s:^:# :' GNUmakefile.in


  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --enable-introspection \
    --disable-geolocation \
    --disable-libsecret \
    --disable-credential_storage \
    --with-font-backend=freetype --disable-gtk-doc \
    --with-unicode-backend=icu \
    --with-gtk=2.0 \
    --disable-webkit2 \
    --enable-spellcheck "$@"


  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
}

package() {
  cd "$srcdir/webkitgtk-$pkgver"
  make DESTDIR="${pkgdir}" install
}
