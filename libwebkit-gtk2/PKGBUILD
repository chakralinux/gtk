# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/webkitgtk
# Contributor on CCR: Ben R <thebenj88 *AT* gmail *DOT* com>

pkgname=libwebkit-gtk2
pkgver=2.4.9
pkgrel=2
pkgdesc="An opensource web content engine"
arch=('x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'icu' 'gst-plugins-base' 'libsoup' 'enchant' 'libwebp' 'harfbuzz-icu')
makedepends=('gperf' 'gobject-introspection' 'python2' 'gtk2' 'ruby')
provides=('libwebkit')
options=('!emptydirs')
install=libwebkit.install
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)
sha256sums=('afdf29e7828816cad0be2604cf19421e96d96bf493987328ffc8813bb20ac564')

build() {

  cd "${srcdir}/webkitgtk-${pkgver}"

  
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --enable-introspection \
    --disable-geolocation \
    --disable-libsecret \
    --disable-credential_storage \
    --with-font-backend=freetype \
    --disable-gtk-doc \
    --with-unicode-backend=icu \
    --with-gtk=2.0 \
    --disable-webkit2 \
    --enable-spellcheck "$@"


  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
}

package() {
  cd "$srcdir/webkitgtk-$pkgver"
  make DESTDIR="${pkgdir}" install
}
