pkgname=flashplugin
pkgver=27.0.0.183
pkgrel=1
pkgdesc='Adobe Flash Player'
url='https://get.adobe.com/flashplayer/'
arch=('x86_64')
depends=('mozilla-common' 'libxt' 'libxpm' 'gtk2' 'nss' 'curl' 'hicolor-icon-theme')
makedepends=('chrpath')
optdepends=('libvdpau: GPU acceleration on Nvidia card')
provides=('flashplayer')
license=('custom')
options=(!strip)
install='flashplugin.install'
backup=('etc/adobe/mms.cfg')
source=("flash_player_npapi_linux_$pkgver.x86_64.tar.gz::https://fpdownload.adobe.com/get/flashplayer/pdc/$pkgver/flash_player_npapi_linux.x86_64.tar.gz"
        'mms.cfg' 'kcm.patch')
md5sums=('4000635d764090e0de272e1056130e51'
         'f34aae6279b40e0bd2abfb0d9963d7b8'
         '798f7d43ed8c0a35e984e51f61b6a1e6')

prepare() {
    patch -Np1 ${srcdir}/usr/share/kde4/services/kcm_adobe_flash_player.desktop kcm.patch
}

package () {
    cd ${srcdir}
   
    msg "Plugin installation"
    install -Dm755 libflashplayer.so "${pkgdir}/usr/lib/mozilla/plugins/libflashplayer.so"
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" license.pdf LGPL/*.txt
    install -Dm644 mms.cfg "${pkgdir}/etc/adobe/mms.cfg"
    install -Dm755 usr/bin/flash-player-properties "${pkgdir}/usr/bin/flash-player-properties"
    install -Dm644 usr/share/applications/flash-player-properties.desktop "${pkgdir}/usr/share/applications/flash-player-properties.desktop"
    for i in 16x16 22x22 24x24 32x32 48x48; do
        install -Dm644 usr/share/icons/hicolor/${i}/apps/flash-player-properties.png \
            "${pkgdir}/usr/share/icons/hicolor/${i}/apps/flash-player-properties.png"
    done

    msg 'KCM install in /usr'
    install -Dm755 usr/lib64/kde4/kcm_adobe_flash_player.so "${pkgdir}/usr/lib/kde4/kcm_adobe_flash_player.so"
    chrpath -d "$pkgdir/usr/lib/kde4/kcm_adobe_flash_player.so"
    install -Dm644 usr/share/kde4/services/kcm_adobe_flash_player.desktop "${pkgdir}/usr/share/kde4/services/kcm_adobe_flash_player.desktop"
}
