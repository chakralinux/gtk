pkgname=flashplugin
_licensefile='PlatformClients_PC_WWEULA_Combined_20100108_1657.pdf'
pkgver=11.2.202.440
pkgrel=2
pkgdesc='Adobe Flash Player'
url='http://labs.adobe.com/technologies/flashplatformruntimes/flashplayer11/'
arch=('x86_64')
depends=('mozilla-common' 'libxt' 'libxpm' 'gtk2' 'nss' 'curl')
optdepends=('libvdpau: GPU acceleration on Nvidia card')
provides=('flashplayer')
license=('custom')
options=(!strip)
install='flashplugin.install'
backup=('etc/adobe/mms.cfg')
source=("flashplugin-${pkgver}::http://fpdownload.macromedia.com/get/flashplayer/pdc/$pkgver/install_flash_player_11_linux.x86_64.tar.gz"
        "http://www.adobe.com/products/eulas/pdfs/${_licensefile}"
        'mms.cfg' 'kcm.patch')
md5sums=('a3dd4401313fa3863b46ad44827db0fd'
         '94ca2aecb409abfe36494d1a7ec7591d'
         'f34aae6279b40e0bd2abfb0d9963d7b8'
         '798f7d43ed8c0a35e984e51f61b6a1e6')

prepare() {
    patch -Np1 ${srcdir}/usr/share/kde4/services/kcm_adobe_flash_player.desktop kcm.patch
}

package () {
    cd ${srcdir}
   
    msg "Plugin installation"
    install -Dm755 libflashplayer.so "${pkgdir}/usr/lib/mozilla/plugins/libflashplayer.so"
    install -Dm644 "${_licensefile}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.pdf"
    install -Dm644 mms.cfg "${pkgdir}/etc/adobe/mms.cfg"
    install -Dm755 usr/bin/flash-player-properties "${pkgdir}/usr/bin/flash-player-properties"
    install -Dm644 usr/share/applications/flash-player-properties.desktop "${pkgdir}/usr/share/applications/flash-player-properties.desktop"
    for i in 16x16 22x22 24x24 32x32 48x48; do
        install -Dm644 usr/share/icons/hicolor/${i}/apps/flash-player-properties.png \
            "${pkgdir}/usr/share/icons/hicolor/${i}/apps/flash-player-properties.png"
    done

    msg 'KCM install in /usr'
    install -Dm755 usr/lib64/kde4/kcm_adobe_flash_player.so "${pkgdir}/usr/lib/kde4/kcm_adobe_flash_player.so"
    install -Dm644 usr/share/kde4/services/kcm_adobe_flash_player.desktop "${pkgdir}/usr/share/kde4/services/kcm_adobe_flash_player.desktop"
}
