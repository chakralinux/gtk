pkgname=amule
pkgver=10958
pkgrel=1
pkgdesc="An eMule-like client for ed2k p2p network"
arch=('x86_64')
url="http://www.amule.org"
license=('GPL')
depends=('wxgtk' 'wxgtk2.8' 'gd' 'geoip' 'libupnp' 'crypto++' 'libsm')
makedepends=('ccache')
install=amule.install
source=("$pkgname-$pkgver.tar.gz::http://repo.or.cz/w/amule.git/snapshot/e539f94e3cdee138cd0248df131c13b2d49d7c45.tar.gz"
        "http://chakra.sourceforge.net/sources/oxygen-themes/aMule_Oxygen_Skin.zip"
        'amuled.systemd'
        'amuleweb.systemd')
sha256sums=('f9eda6c0ede1d76f1d9c7c5564e94a156e68a4ad6c1362408583ed4cc0687221'
            'a340cadb86697acde60b94187fe0134d5262635233870e583a766aa1b8aa06ad'
            '527a2cca6d56b8269722aafc77a89734f59a929f946f342bd0f69b256f9c7522'
            'f4f43b1154ddccc9036a4291a58c6715f097b171fec62ea7aead0c9d9fa654f2')

build() {
  cd $pkgname-e539f94

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-cas \
              --enable-wxcas \
              --enable-amule-daemon \
              --enable-amulecmd \
              --enable-amule-gui \
              --enable-alc \
              --enable-alcc \
              --enable-webserver \
              --disable-debug \
              --enable-optimize \
              --enable-profile \
              --enable-mmap \
              --enable-ccache \
              --enable-geoip \
              --enable-upnp \
              --with-wxversion=2.8

  make
}

package() {
  cd $pkgname-e539f94

  make DESTDIR=$pkgdir install

  cp -f ../aMule_Oxygen_Skin.zip $pkgdir/usr/share/amule/skins/

  install -D -m644 ../amuled.systemd $pkgdir/usr/lib/systemd/system/amuled.service
  install -D -m644 ../amuleweb.systemd $pkgdir/usr/lib/systemd/system/amuleweb.service
}
