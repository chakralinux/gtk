# Maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>

pkgname=amule
pkgver=10834
pkgrel=1
pkgdesc="An eMule-like client for ed2k p2p network"
arch=('x86_64')
url="http://www.amule.org"
license=('GPL')
depends=('wxgtk' 'gd' 'geoip' 'libupnp' 'crypto++' 'libsm')
makedepends=('ccache')
install=amule.install
source=("http://amule.sourceforge.net/tarballs/aMule-SVN-r${pkgver}.tar.bz2"
        "http://chakra.sourceforge.net/sources/oxygen-themes/aMule_Oxygen_Skin.zip"
        'amuled.systemd'
        'amuleweb.systemd')
md5sums=('80e6375acbdc287b15cc92131bf9e027'
         '9c1c06c1f7eaffd5dd383b7e0348b6db'
         '59772c41860e238f1c822feb8ca8d47f'
         '05975c5d94bfc41fddb894d98b1115d5')


build() {
  cd "${srcdir}/aMule-SVN-r${pkgver}"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-cas \
              --enable-wxcas \
              --enable-amule-daemon \
              --enable-amulecmd \
              --enable-amule-gui \
              --enable-alc \
              --enable-alcc \
              --enable-webserver \
              --disable-debug \
              --enable-optimize \
              --enable-ccache \
              --enable-geoip \
              --disable-plasmamule \
              --enable-upnp

  make
}

package() {
  cd "${srcdir}/aMule-SVN-r${pkgver}"

  make DESTDIR=${pkgdir}/ install

  cp -f $srcdir/aMule_Oxygen_Skin.zip $pkgdir/usr/share/amule/skins/

  install -D -m644 "${srcdir}/amuled.systemd" "${pkgdir}/usr/lib/systemd/system/amuled.service"
  install -D -m644 "${srcdir}/amuleweb.systemd" "${pkgdir}/usr/lib/systemd/system/amuleweb.service"
}
