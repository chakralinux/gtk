
pkgname=libgexiv2
pkgver=0.10.8
pkgrel=2
pkgdesc="GObject-based wrapper around the Exiv2 library"
url="https://wiki.gnome.org/Projects/gexiv2"
arch=(x86_64)
license=(GPL2)
depends=('exiv2' 'glib2')
makedepends=('python3-gobject' 'python2-gobject' 'gobject-introspection' 'vala' 'git' 'meson')
_commit=f1bdbe17d6743da27c1717d8f4d2793a05c72277  # tags/gexiv2-0.10.8^0
source=("git+https://git.gnome.org/browse/gexiv2#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gexiv2
  git describe --tags | sed 's/^gexiv2-//;s/-/+/g'
}

build() {
  chakra-meson gexiv2 build \
    -Denable-gtk-doc=false
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR=$pkgdir ninja -C build install
}
