pkgname=veracrypt
pkgver=1.21
pkgrel=1
pkgdesc="Free disk encryption software, TrueCrypt fork."
arch=('x86_64')
url='https://www.veracrypt.fr'
license=('custom')
depends=('fuse' 'wxgtk' 'libsm' 'device-mapper')
makedepends=('nasm')
replaces=('truecrypt')
install='veracrypt.install'
source=("https://github.com/veracrypt/VeraCrypt/archive/VeraCrypt_${pkgver}.tar.gz"
	'no_makeself.patch')
sha256sums=('4071d3d1e87839a7c2bdc9a3837940d49f38bfd92d71f9cbc670b61a48824885'
            '76c78498703d806d80707e65b253c2f2fa7f328fdae29efdca64b6d4a0ef2816')

build() {
  cd ${srcdir}/VeraCrypt-VeraCrypt_${pkgver}/src
  # apply patch(es)
  patch -Np1 -i ../../no_makeself.patch  # disable sfx https://github.com/veracrypt/VeraCryptarchive
  # build
  export WX_CONFIG=/usr/bin/wx-config
  make LFLAGS+="-ldl"
}

package() {
  cd ${srcdir}/VeraCrypt-VeraCrypt_${pkgver}/src
  install -D -m755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 "Setup/Linux/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 Resources/Icons/VeraCrypt-256x256.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -D -m644 "Release/Setup Files/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "Release/Setup Files/VeraCrypt User Guide.pdf" "${pkgdir}/usr/share/${pkgname}/doc/VeraCrypt User Guide.pdf"
}
