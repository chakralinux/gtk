pkgname=veracrypt
pkgver=1.16
pkgrel=1
pkgdesc="Free disk encryption software, TrueCrypt fork."
arch=('x86_64')
url="http://veracrypt.codeplex.com/"
license=('custom')
depends=('fuse' 'wxgtk' 'libsm' 'device-mapper')
makedepends=('nasm')
replaces=('truecrypt')
conflicts=('truecrypt')
provides=('truecrypt')
install='veracrypt.install'
source=("https://github.com/veracrypt/VeraCrypt/archive/VeraCrypt_${pkgver}.tar.gz"
	'no_makeself.patch'
        'veracrypt.desktop'
        'veracrypt.install'
        'veracrypt.png')
sha512sums=('9f924de8bb4f6e081e167447d9d0666e14110c9b08edcc78c874033fbef4e0f3e7549034e7999e4f9c7ab1176bd95957cf8a43fc6c8b5ec441c22813d7604684'
	  'e3972fd239fd83807c5ea9dd9268a0e962b0bd9cb9cf8f6251ac51a44a94f3c57f0b1e6857b42e4296cff531030c8278c543b386ebf463d4e19fe9a536acbcf9'
          '4a684900d471b1f3241463bc3d2ac944938fc93b48e2acd9f6f7dc15b3756964e42f7a7ab4ddcc701b537b0e3f92c54ed95aaa292ea336cf2488da7ce83e03e4'
          '1aed149361129cc2918e8b7548fd6a7a0cf5a13535cfd5aaf403718224b6e004a61cf8ac65ca3fbb90644338842dc0780285ace80b82b071bd39c2a0540a4b1a'
          '424673b2f0abf2830dd16095caa413dfedff7018fca41ef5ce8e62312ec6ebce6cc23c8477bffc768c5d3b1e1d979457531a968886f68720e626be85845e4fb3')

build() {
  cd ${srcdir}/VeraCrypt-VeraCrypt_${pkgver}/src
  # apply patch(es)
  patch -Np1 -i ../../no_makeself.patch  # disable sfx https://github.com/veracrypt/VeraCryptarchive
  # build
  export WX_CONFIG=/usr/bin/wx-config
  make LFLAGS+="-ldl"
}

package() {
  cd ${srcdir}/VeraCrypt-VeraCrypt_${pkgver}/src
  install -D -m755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 ../../veracrypt.png "${pkgdir}/usr/share/pixmaps/veracrypt.png"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
