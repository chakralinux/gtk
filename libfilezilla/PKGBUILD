# Contributions from Arch:
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

pkgname=libfilezilla
pkgver=0.9.2
pkgrel=2
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs"
arch=('x86_64')
url="https://lib.filezilla-project.org/"
license=('GPL')
depends=('gcc-libs')
source=("https://download.filezilla-project.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        libfilezilla-0.9.2-util.patch)
md5sums=('d52a0e7229efb3c9fafc8a2b649d665d'
         'b8bf019f63e34dd84df5ab5867b3fa64')

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"
  patch -Np2 -i ../libfilezilla-0.9.2-util.patch
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
