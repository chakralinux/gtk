
pkgname=libmypaint
pkgver=1.3.0
pkgrel=1
pkgdesc='Library for making brushstrokes which is used by MyPaint and other projects, with GEGL support'
url='http://mypaint.org/'
arch=('x86_64')
license=('ISC')
depends=('glibc' 'json-c' 'gegl' 'glib2' 'json-glib' 'babl')
makedepends=('intltool' 'python' 'gobject-introspection')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mypaint/libmypaint/archive/v${pkgver}.tar.gz"
        "versioning.patch" 
        "gegl-0.4.patch")
sha512sums=('98ba7a48a611014e683d8d534c73af1ed0754e1d9974d62db7efb588abdb9c01d090daaaba3847ece7ca7e5a4e83b334f2a3473a2c9b7e4a9c0680231f650202'
            '918541f71a7c8aaf9fbb5873d8e3fb54c2409316ec5ed10a60b7e44c31af3360f81c8cf981dd474951103cce656943e68a7c3666d8bf9ee33a2d26ce2d6a1a64'
            'e7a79f1d8992022e31e5c24f36684327ec465b48a23e967e7ae290b8a7f35a95a2e3676ca2281d72e2533ae1edb643a5bf827cc7550fb7a39d6bbc14f334c713')

prepare() {
  cd $pkgname-$pkgver
  patch -uNp2 < ../versioning.patch
  patch -Np1 < ../gegl-0.4.patch
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-gegl
  make
}

check() {
  cd $pkgname-$pkgver
  make check || warning "Failed to execute tests (make problem)"
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm 644 README.md -t $pkgdir/usr/share/doc/$pkgname
  install -Dm 644 COPYING -t $pkgdir/usr/share/licenses/$pkgname
} 
