# Maintainer: Manuel Tortosa <manutortos@chakra-project.org>

pkgname=filesystem-extra
pkgver=2013.05
pkgrel=5
url="http://chakra-project.org"
license="GPLv2"
arch=('any')
pkgdesc="Extra filesystem"
makedepends=('python2' 'python3')
groups=('extra')
install=extra.install
source=('extra.conf' 'extra.profile' 'extra.tcsh' 'get_site_directory.py')
md5sums=('b0f054b6c5f809927817b345351ed7b6'
         'a2834ec1cb203cbb67a5a82460aa4b0a'
         '5b3f072290c29ebd4989303f8a44e998'
         '265cffea08e37a0651b2296dedd159d3')

package() {
  cd ${pkgdir}

  install -d -m755 extra
  install -d -m755 extra/etc
  install -d -m755 extra/opt

  # Setup /extra/usr hierarchy
  for d in bin sbin lib share/misc include src; do	
    install -d -m755 extra/usr/${d}	
  done
  for d in $(seq 8); do	
    install -d -m755 extra/usr/share/man/man${d}	
  done
  
  # Add a profile for extra
  install -D -m755 "$srcdir/extra.profile" "$pkgdir/etc/profile.d/extra.sh"
  install -D -m755 "$srcdir/extra.tcsh" "$pkgdir/etc/profile.d/extra.csh"

  # Add /extra/usr/lib to the default library search path
  install -Dm644 "$srcdir/extra.conf" "$pkgdir/etc/ld.so.conf.d/extra.conf"

  # Add /extra/usr/lib/python-${version}/site-packages to python site-packages directory
  PYTHON2_SITE_DIRECTORY=`python2 "$srcdir/get_site_directory.py"`
  PYTHON3_SITE_DIRECTORY=`python3 "$srcdir/get_site_directory.py"`

  cat > extra-python2.pth <<EOF
/extra$PYTHON2_SITE_DIRECTORY
/extra$PYTHON2_SITE_DIRECTORY/gtk-2.0
EOF

  cat > extra-python3.pth <<EOF
/extra$PYTHON3_SITE_DIRECTORY
/extra$PYTHON3_SITE_DIRECTORY/gtk-2.0
EOF

  install -Dm644 extra-python2.pth "$pkgdir/$PYTHON2_SITE_DIRECTORY/extra.pth"
  install -Dm644 extra-python3.pth "$pkgdir/$PYTHON3_SITE_DIRECTORY/extra.pth"
}
