# Maintainer: Weng Xuetian <wengxt@gmail.com>

_pkgbase=fcitx
pkgbase=fcitx-gtk
pkgname=('fcitx-gtk2' 'fcitx-gtk3')
pkgver=4.2.8.4
pkgrel=1
pkgdesc="Gtk im module for fcitx"
arch=('i686' 'x86_64')
url="https://fcitx-im.org/wiki/Fcitx"
license=('GPL')
makedepends=('gtk2' 'gtk3' 'cmake' 'icu' 'opencc' 'iso-codes' 'libxkbfile' 'cairo' 'pango' 'dbus-core' 'qt')
source=(https://github.com/fcitx/fcitx/archive/${pkgver}.tar.gz)
md5sums=('d915202c5a4437e5e1a70eb7b7e69a41')

prepare() {
    cd ${srcdir}/${_pkgbase}-${pkgver}
    [[ -e build ]] && rm -rf build
    mkdir build
}
build(){
    cd ${srcdir}/${_pkgbase}-${pkgver}/build
    cmake -DENABLE_GTK3_IM_MODULE=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
	  -DENABLE_LUA=On \
	  -DENABLE_GIR=Off \
          -DCMAKE_BUILD_TYPE=Release ..
    make
}

package_fcitx-gtk2 ()
{
    pkgdesc='GTK2 IM Module for fcitx'
    depends=('gtk2' "fcitx>=$pkgver")
    install="fcitx-gtk2.install"
    cd "${srcdir}/${_pkgbase}-${pkgver}/build/src/frontend/gtk2"
    make DESTDIR=${pkgdir} install
}

package_fcitx-gtk3 ()
{
    pkgdesc='GTK3 IM Module for fcitx'
    depends=('gtk3' "fcitx>=$pkgver")
    install="fcitx-gtk3.install"
    cd "${srcdir}/${_pkgbase}-${pkgver}/build/src/frontend/gtk3"
    make DESTDIR=${pkgdir} install
}
