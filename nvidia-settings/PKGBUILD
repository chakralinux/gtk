# maintainer: AlmAck <gluca86@gmail.com>

pkgname=nvidia-settings
_pkgbasename=nvidia
pkgver=387.22
pkgrel=1
arch=('x86_64')
pkgdesc="Gtk Graphical User Interface for nVidia"
depends=('gtk3' 'pangox-compat' $_pkgbasename=$pkgver)
optdepends=('gtk2: use gtk2 for interface')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
#source+=("NVIDIA-Linux-x86_64-${pkgver}.run::https://developer.nvidia.com/linux64bit")

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"

source+=('nvidia-restore-settings.desktop')
md5sums=('b21f9bafb20409b337505c9b1d362c34'
         '9237516792da7b6df32f8c7e8a98f475')

build() {
  sh "${_pkg}.run" --extract-only
}

package() {
  cd "${srcdir}/${_pkg}"

  install -D -m755 nvidia-settings "${pkgdir}/usr/bin/nvidia-settings"
  install -D -m644 nvidia-settings.1.gz "${pkgdir}/usr/share/man/man1/nvidia-settings.1.gz"
  install -D -m644 nvidia-settings.desktop "${pkgdir}/usr/share/applications/nvidia-settings.desktop"
  install -D -m644 nvidia-settings.png "${pkgdir}/usr/share/pixmaps/nvidia-settings.png"
  install -D -m755 "libnvidia-gtk2.so.$pkgver" "$pkgdir/usr/lib/libnvidia-gtk2.so.$pkgver"
  install -D -m755 "libnvidia-gtk3.so.$pkgver" "$pkgdir/usr/lib/libnvidia-gtk3.so.$pkgver"
  sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i "${pkgdir}/usr/share/applications/nvidia-settings.desktop"

  # Install the autostart .desktop file, so that parameters saved through nvidia-settings
  # are automatically reloaded at each login
  install -D -m644 "${srcdir}/nvidia-restore-settings.desktop" "${pkgdir}/etc/xdg/autostart/nvidia-restore-settings.desktop"
}
