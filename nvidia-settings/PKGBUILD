#
# Chakra Packages for Chakra, part of chakra-project.org

pkgbase=nvidia-utils
pkgname=('nvidia-settings')
pkgdesc="Gtk Graphical User Interface for nVidia"
depends=('gtk2' 'pangox-compat')
pkgver=319.32
pkgrel=2
arch=('x86_64')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run"
        'nvidia-restore-settings.desktop')
md5sums=('d181d856768868823df3bda4607a5315'
         'e69b7514b01c1e025b927923e9267c9e')

build() {
  cd "${srcdir}"
  sh "NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run" --extract-only
}

package() {
  cd "${srcdir}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

  install -D -m755 nvidia-settings "${pkgdir}/usr/bin/nvidia-settings"
  install -D -m644 nvidia-settings.1.gz "${pkgdir}/usr/share/man/man1/nvidia-settings.1.gz"
  install -D -m644 nvidia-settings.desktop "${pkgdir}/usr/share/applications/nvidia-settings.desktop"
  install -D -m644 nvidia-settings.png "${pkgdir}/usr/share/pixmaps/nvidia-settings.png"
  sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i "${pkgdir}/usr/share/applications/nvidia-settings.desktop"

  # Install the autostart .desktop file, so that parameters saved through nvidia-settings
  # are automatically reloaded at each login
  install -D -m644 "${srcdir}/nvidia-restore-settings.desktop" "${pkgdir}/etc/xdg/autostart/nvidia-restore-settings.desktop"
}
