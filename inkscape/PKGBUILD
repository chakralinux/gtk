# Contributions from ArchLinux: https://git.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/inkscape

pkgname=inkscape
pkgver=0.92.4
pkgrel=1
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('x86_64')
depends=('gc' 'gsl' 'gtkmm' 'gtkspell' 'libmagick6' 'libxslt' 'libvisio' 'libwpg'
         'poppler-glib' 'popt' 'potrace' 'ttf-dejavu' 'python2' 'desktop-file-utils'
         'hicolor-icon-theme' 'openmp')
# python is needed for glib-genmarshal (at least with GLib 2.54)
makedepends=('boost' 'intltool' 'python3' 'clang')
optdepends=('pstoedit: latex formulas'
            'python2-scour: optimized SVG output'
            'texlive-core: latex formulas'
            'python2-numpy: some extensions'
            'python2-lxml: some extensions and filters'
            'uniconvertor: reading/writing to some proprietary formats')
source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('57ec2da8177b36614a513e2822efd73af721e690f7ddc6bd0a5fbb1525b4515e')

prepare() {
	cd $pkgname-$pkgver

	./autogen.sh

	sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
	sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
	sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
	sed -i 's|python -c|python2 -c|g' configure share/extensions/uniconv*.py
	sed -i 's|"python"|"python2"|g' src/main.cpp

	sed -i 's| abs(| std::fabs(|g' src/ui/tools/flood-tool.cpp
       
}

build() {
	cd $pkgname-$pkgver
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
	export FREETYPE_CONFIG="/usr/bin/pkg-config freetype2"
        export CC=clang
        export CXX=clang++
	./configure \
		--prefix=/usr \
		--enable-lcms \
		--enable-poppler-cairo \
		--disable-strict-build \
		--disable-dependency-tracking \
                --with-inkjar \
                --enable-wpg \
                --enable-visio \
                --enable-dbusapi \
                --enable-openmp \
                --disable-static
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="${pkgdir}" install
}
