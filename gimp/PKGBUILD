pkgname=gimp
pkgver=2.8.22
_pkgver=2.8
pkgrel=2
pkgdesc="GNU Image Manipulation Program (Development Version)"
arch=('x86_64')
url="http://www.gimp.org/"
license=('GPL3' 'LGPL3')
depends=('libpng' 'pygtk' 'lcms' 'libxpm' 'libwmf' 'librsvg' 'libmng' 'dbus-glib' \
         'jasper' 'libexif' 'gegl' 'desktop-file-utils' 'hicolor-icon-theme' 'babl')
makedepends=('intltool' 'libwebkit-gtk2' 'poppler-glib' 'alsa-lib' 'iso-codes' 'curl' 'ghostscript')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support')
options=('!makeflags')
install=gimp.install
source=("http://download.gimp.org/pub/gimp/v${_pkgver}/gimp-${pkgver}.tar.bz2" "linux.gpl")
md5sums=('7e4fd7a53b1d3c32dff642ab1a94b44d'
         'bb27bc214261d36484093e857f015f38')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/gimp-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-mp \
    --enable-gimp-console \
    --enable-python \
    --with-libcurl \
    --with-gif-compression=lzw \
    --with-gimpdir=.config/gimp \
    --without-aa \
    --without-hal \
    --without-gvfs \
    --without-gnomevfs
  make
}

package() {
  cd "${srcdir}/gimp-${pkgver}"
  make DESTDIR="${pkgdir}" install
  sed -i 's|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  ln -sf gimp-console-${pkgver}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -sf gimprc-${pkgver}.5.gz "${pkgdir}/usr/share/man/man5/gimprc.5.gz"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
  ln -s gimp-${pkgver}.1.gz "${pkgdir}/usr/share/man/man1/gimp.1.gz"
}
