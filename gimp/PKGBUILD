pkgname=gimp
pkgver=2.8.4
_pkgver=2.8
pkgrel=6
pkgdesc="GNU Image Manipulation Program (Development Version)"
arch=('x86_64')
url="http://www.gimp.org/"
license=('GPL3' 'LGPL3')
depends=('pygtk' 'lcms' 'libxpm' 'libwmf' 'librsvg' 'libmng' 'dbus-glib' 'filesystem-extra' \
         'jasper' 'libexif' 'gegl' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'libwebkit' 'poppler-glib' 'alsa-lib' 'iso-codes' 'curl' 'ghostscript')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'libwebkit-gtk2: for the help browser or web-page plug-ins'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support')
options=('!libtool' '!makeflags')
conflicts=('gimp')
provides=("gimp=${pkgver}")
install=gimp.install
source=(ftp://ftp.gimp.org/pub/gimp/v${_pkgver}/gimp-${pkgver}.tar.bz2 linux.gpl)
md5sums=('392592e8755d046317878d226145900f'
         'bb27bc214261d36484093e857f015f38')

build() {
  cd "${srcdir}/gimp-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/extra/usr --sysconfdir=/extra/etc \
    --enable-mp --enable-gimp-console --enable-python --with-libcurl \
    --with-gif-compression=lzw --without-aa --without-gvfs
  make
}

package() {
  cd "${srcdir}/gimp-${pkgver}"
  make DESTDIR="${pkgdir}" install
  sed -i 's|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/extra/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/extra/usr/share/gimp/2.0/palettes/Linux.gpl"

  ln -sf gimp-console-${pkgver}.1.gz "${pkgdir}/extra/usr/share/man/man1/gimp-console.1.gz"
  ln -sf gimprc-${pkgver}.5.gz "${pkgdir}/extra/usr/share/man/man5/gimprc.5.gz"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/extra/usr/share/man/man1/gimptool.1.gz"
  ln -s gimp-${pkgver}.1.gz "${pkgdir}/extra/usr/share/man/man1/gimp.1.gz"
}
