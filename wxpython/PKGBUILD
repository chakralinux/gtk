# Maintainer: Jeff Haung <s8321414[at]gmail[dot]com>
# Original maintainer on CCR: Kyle Devir (QuartzDragon) <kyle.devir@gmx.com>

pkgname=wxpython
pkgver=3.0.2.0
pkgrel=2
pkgdesc="A wxWidgets GUI toolkit for Python"
arch=('x86_64')
url="http://wxpython.org"
license=('custom:wxWindows')
depends=('wxgtk' 'python2')
makedepends=('mesa' 'glu')
provides=('wxpython')
options=('!emptydirs')
source=("https://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2")
md5sums=('922b02ff2c0202a7bf1607c98bbbbc04')

build() {
  cd wxPython-src-${pkgver}
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --includedir=/usr/include \
              --with-gtk=3 \
              --with-opengl \
              --enable-unicode \
              --enable-graphics_ctx \
              --enable-mediactrl \
              --enable-stl \
              --with-regex=builtin \
              --with-libpng=sys \
              --with-libxpm=sys \
              --with-libjpeg=sys \
              --with-libtiff=sys \
              --disable-precomp-headers \
              --without-gconf
  cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
  python2 setup.py WXPORT=gtk3 UNICODE=1 build
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
  python2 setup.py WXPORT=gtk3 UNICODE=1 install --root="${pkgdir}"
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
