pkgname=('gcompris')
pkgver=13.11
pkgrel=2
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('x86_64')
url="http://gcompris.net/"
license=('GPL3')
depends=('pygtk' 'python-pyxml' 'librsvg' 'python-pysqlite'
           'gstreamer0.10-base-plugins')
makedepends=('texinfo' 'texi2html' 'intltool' 'gettext' 'gnuchess')
optdepends=('gnucap: for computer simulation within the electricity activity'
            'tuxpaint: for the painting activity'
            'gnuchess: for the chess activity')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('7c3396c02e44bbf72424ca12bfff9cf3')
 
build() {
  cd "$srcdir/$pkgname-$pkgver"
 
  ./configure --prefix=/usr
  make
}
 
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Fix path for editor
  sed -i -e "~s~gcompris -a~gcompris -l /usr/lib/gcompris/ -a~" ${pkgdir}/usr/share/applications/gcompris-edit.desktop
}
