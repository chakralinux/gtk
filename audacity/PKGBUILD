pkgname=audacity
pkgver=2.1.3
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk' 'lame' 'lilv' 'soundtouch' 'libsamplerate'
         'ffmpeg' 'vamp-plugin-sdk' 'sbsms' 'portsmf' 'desktop-file-utils' 'jack')
makedepends=('python2' 'cmake' 'portaudio')
options=('!makeflags')
install="${pkgname}.install"
source=($pkgname::git+https://github.com/audacity/audacity.git#commit=cf68891
	audacity-ffmpeg.patch)
sha256sums=('SKIP'
            'ee5ddddee23cd292167d319e0dc61a96fb1477380ffbb225d4103472f7aa69b4')

prepare() {
  cd ${pkgname}
  patch -p1 -i "${srcdir}/audacity-ffmpeg.patch"
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr \
              --with-libsamplerate \
              --enable-shared
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}
