pkgname=oxygen-gtk2
pkgver=1.4.6
pkgrel=2
pkgdesc="The Oxygen engine for GTK2, for better Qt/GTK+ integration."
arch=('x86_64')
url=("https://projects.kde.org/projects/playground/artwork/oxygen-gtk")
license=('LGPL')
depends=('cairo' 'gtk2')
makedepends=('cmake')
categories=('system')
groups=('gtk-integration')
install=install
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('493892fc36302bfe862604f667a6c04e')

prepare() {
  if [[ -e build ]]; then rm -rf build; fi
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DOXYGEN_ICON_HACK=0 \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
