pkgname=wxgtk
pkgver=3.0.0
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base')
makedepends=('gstreamer0.10-base-plugins' 'glu')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2)
sha1sums=('756a9c54d1f411e262f03bacb78ccef085a9880a')

# makedepends=('webkitgtk2')
# optdepends=('webkitgtk2: for webview support')
# --enable-webview

build() {
  cd wxWidgets-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-stl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers --without-gconf
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
