pkgname=wxgtk
pkgver=3.0.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base')
makedepends=('gstreamer0.10-base-plugins' 'glu')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
sha256sums=('bd671b79ec56af8fb3844e11cafceac1a4276fb02c79404d06b91b6c19d2c5f5')

# makedepends=('webkitgtk2')
# optdepends=('webkitgtk2: for webview support')
# --enable-webview

build() {
  cd wxWidgets-${pkgver}
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --with-gtk=2 \
    --with-opengl \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-mediactrl \
    --enable-stl \
    --with-regex=builtin \
    --with-libpng=sys \
    --with-libxpm=sys \
    --with-libjpeg=sys \
    --with-libtiff=sys \
    --disable-precomp-headers \
    --without-gconf
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
