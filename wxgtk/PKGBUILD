pkgname=wxgtk
pkgver=3.0.2
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base')
makedepends=('gstreamer0.10-base-plugins' 'glu')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
sha256sums=('346879dc554f3ab8d6da2704f651ecb504a22e9d31c17ef5449b129ed711585d')

# makedepends=('webkitgtk2')
# optdepends=('webkitgtk2: for webview support')
# --enable-webview

build() {
  cd wxWidgets-${pkgver}
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --with-gtk=2 \
    --with-opengl \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-mediactrl \
    --with-regex=builtin \
    --with-libpng=sys \
    --with-libxpm=sys \
    --with-libjpeg=sys \
    --with-libtiff=sys \
    --disable-precomp-headers \
    --without-gconf
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
