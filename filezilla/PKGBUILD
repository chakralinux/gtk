pkgname=filezilla
pkgver=3.29.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL')
depends=('dbus' 'xdg-utils' 'wxgtk' 'libidn' 'hicolor-icon-theme' 'sqlite3' 'gnutls' 'libfilezilla' 'libnotify')
install=${pkgname}.install
screenshot="https://filezilla-project.org/images/screenshots/fz3_linux_main.png"
source=("http://downloads.sourceforge.net/project/filezilla/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_src.tar.bz2")
sha256sums=('ead1ed74f19cf33aadf814a45b742207de3a8c349dbe2a11c344966bb8705259')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --enable-locales \
    --enable-gnutlssystemciphers \
    --with-pugixml=builtin

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 {AUTHORS,ChangeLog,README} "${pkgdir}/usr/share/doc/${pkgname}/"
}
