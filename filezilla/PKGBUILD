pkgname=filezilla
pkgver=3.8.1
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('x86_64')
url="http://filezilla-project.org/"
license=('GPL')
depends=('dbus-core' 'xdg-utils' 'wxgtk2.8' 'libidn' 'libsm' 'libxxf86vm' 
         'hicolor-icon-theme' 'sqlite3')
install=${pkgname}.install
changelog=${pkgname}.changelog
source=("http://downloads.sourceforge.net/project/filezilla/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_src.tar.bz2"
        "http://kde-look.org/CONTENT/content-files/141546-filezilla-oxygen-theme.tar.gz")
sha256sums=('66bc9c082843ffdcb915c7902900b92bdf921ed7310f2a40ff6b96b03cfa1e4f'
            '3943d1f650e6d1bece2ba1fdd84d013b9f8fabdcfc6845f9d62e5d4673a21740')

build() {
  cd "${pkgname}-${pkgver}"
  export WX_CONFIG_PATH='/usr/bin/wx-config-2.8'
  export WXRC='/usr/bin/wxrc-2.8'
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-tinyxml=builtin

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/filezilla/resources/oxygen"
  cp -dpr --no-preserve=ownership "${srcdir}/oxygen" "${pkgdir}/usr/share/filezilla/resources/"
  
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 {AUTHORS,ChangeLog,README} "${pkgdir}/usr/share/doc/${pkgname}/"
}
