pkgname=filezilla
pkgver=3.19.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('x86_64')
url="http://filezilla-project.org/"
license=('GPL')
depends=('dbus' 'xdg-utils' 'wxgtk' 'libidn' 'hicolor-icon-theme' 'sqlite3' 'gnutls' 'libfilezilla>=0.5.2')
install=${pkgname}.install
screenshot="https://filezilla-project.org/images/screenshots/fz3_linux_main.png"
source=("http://downloads.sourceforge.net/project/filezilla/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_src.tar.bz2")
sha256sums=('82a68742a02726d5559e3193e5c7d96f0f9105f4053e7959fef3225b65f84720')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --enable-locales \
    --enable-gnutlssystemciphers \
    --with-pugixml=builtin

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 {AUTHORS,ChangeLog,README} "${pkgdir}/usr/share/doc/${pkgname}/"
}
