pkgname=filezilla
pkgver=3.40.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL')
depends=('dbus' 'xdg-utils' 'wxgtk' 'libidn' 'hicolor-icon-theme' 'sqlite3' 'gnutls' 'libfilezilla' 'libnotify')
source=("https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2")
sha256sums=('c36ca08540f0be2227b8721195bb16a626c01608f09ed60a862a73e640206085')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --enable-locales \
    --enable-gnutlssystemciphers \
    --with-pugixml=builtin

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 {AUTHORS,ChangeLog,README} "${pkgdir}/usr/share/doc/${pkgname}/"
}
