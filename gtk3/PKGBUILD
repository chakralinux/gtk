pkgname=gtk3
pkgver=3.10.7
pkgmin="${pkgver%.*}"
pkgrel=2
pkgdesc="The GTK+ Toolkit (v3)"
arch=('x86_64')
url="http://www.gtk.org/"
install="gtk3.install"
depends=('glib2' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor'
         'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
         'pango' 'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland' 'libxkbcommon')
makedepends=('gobject-introspection')
options=('!docs')
backup=("etc/gtk-3.0/settings.ini")
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgmin}/gtk+-${pkgver}.tar.xz"
        'settings.ini')
md5sums=('18a81944a8506231529a76bf2b68372b'
         '6bbfc6cc58bae5b1be02e60b48fa51f5')

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  CXX=/bin/false ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-gtk2-dependency \
      --disable-schemas-compile \
      --enable-x11-backend \
      --enable-broadway-backend \
      --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/settings.ini" \
                   "${pkgdir}/etc/gtk-3.0/settings.ini"
}
