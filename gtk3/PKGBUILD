pkgname=gtk3
pkgver=3.14.8
pkgmin="${pkgver%.*}"
pkgrel=2
pkgdesc="The GTK+ Toolkit (v3)"
arch=('x86_64')
url="http://www.gtk.org/"
install="gtk3.install"
depends=('glib2' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor'
         'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
         'pango' 'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland' 'libxkbcommon')
makedepends=('gobject-introspection')
options=('!docs')
backup=("etc/gtk-3.0/settings.ini")
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgmin}/gtk+-${pkgver}.tar.xz"
        'settings.ini')
md5sums=('e46a12448b348d384b8bb29f0c22718f'
         '6bbfc6cc58bae5b1be02e60b48fa51f5')

#prepare() {
#  cd "${srcdir}/gtk+-${pkgver}"
  # GLib2
#  sed -e 's|2.43.0|2.42.0|g' -i configure
  # Cairo
#  sed -e 's|1.14.0|1.12.0|g' -i configure
  # Pango
#  sed -e 's|1.36.7|1.36.0|g' -i configure
  # Atk
#  sed -e 's|2.15.1|2.14.0|g' -i configure
#}

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  CXX=/bin/false ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-schemas-compile \
      --enable-x11-backend \
      --enable-broadway-backend \
      --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/settings.ini" \
                   "${pkgdir}/etc/gtk-3.0/settings.ini"

  mv -v ${pkgdir}/usr/bin/gtk-update-icon-cache ${pkgdir}/usr/bin/gtk3-update-icon-cache
}
