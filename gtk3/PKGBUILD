pkgname=gtk3
pkgver=3.18.6
pkgmin="${pkgver%.*}"
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3)"
arch=('x86_64')
url="http://www.gtk.org/"
install="gtk3.install"
depends=('glib2>=2.45.8' 'cairo>=1.14.0' 'gtk-update-icon-cache' 'libcups' 'libxcursor'
         'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'gdk-pixbuf2>=2.30.0'
         'pango>=1.37.3' 'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland' 'libxkbcommon')
makedepends=('gobject-introspection' 'python2')
options=('!docs')
backup=("etc/gtk-3.0/settings.ini")
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgmin}/gtk+-${pkgver}.tar.xz"
        'settings.ini')
sha256sums=('78cabf0fd5a662f8723f62d5ac633072c76c557c1d700454c9c3deaa37e441ef'
            '0dd1d69a8e9ca9fca03b874049e0a7a439c4100cb4aac4dc37d12b595579c9d9')

#prepare() {
#  cd "${srcdir}/gtk+-${pkgver}"
  # GLib2
#  sed -e 's|2.43.0|2.42.0|g' -i configure
  # Cairo
#  sed -e 's|1.14.0|1.12.0|g' -i configure
  # Pango
#  sed -e 's|1.36.7|1.36.0|g' -i configure
  # Atk
#  sed -e 's|2.15.1|2.14.0|g' -i configure
#}

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  CXX=/bin/false ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-schemas-compile \
      --enable-x11-backend \
      --enable-broadway-backend \
      --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/settings.ini" \
                   "${pkgdir}/etc/gtk-3.0/settings.ini"

  rm -v ${pkgdir}/usr/bin/gtk-update-icon-cache
}
