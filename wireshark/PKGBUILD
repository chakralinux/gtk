# maintainer: AlmAck

pkgname=wireshark
pkgver=1.10.3
pkgrel=1
pkgdesc='A free network protocol analyzer for Unix/Linux and Windows'
arch=('x86_64')
license=('GPL2')
depends=('libpcap' 'libcap' 'krb5' 'glib2' 'desktop-file-utils' 'hicolor-icon-theme' 'lua' 'libgcrypt' 'gtk2' 'filesystem-extra')
makedepends=('bison' 'flex' 'bash' 'gnutls' 'python' 'portaudio')
url='http://www.wireshark.org/'
install=${pkgname}.install
options=('!libtool')
source=("http://www.wireshark.org/download/src/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('c1641b36aeb0ca2c1a95fb01d2c647888b65e00a215c9a6464b5fc61e0fd498c')

build() {
  cd ${pkgname}-${pkgver}
  
  ./autogen.sh
  ./configure \
      --prefix=/extra/usr \
      --with-ssl \
      --with-pcap \
      --with-libcap \
      --with-zlib \
      --with-lua \
      --with-krb5 \
      --without-portaudio
  make all
}

package() {

  ### GTK cli ###
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/extra/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/extra/usr/bin/dumpcap"
  #rm "${pkgdir}/extra/usr/bin/wireshark"

  # Headers
  install -dm755 "${pkgdir}"/extra/usr/include/${pkgname}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

  install -m644 color.h config.h register.h "${pkgdir}/extra/usr/include/${pkgname}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}"/extra/usr/include/${pkgname}/${d}
  done
  
  
  ### GTK GUI ###
  #install -Dm755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/extra/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/extra/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/extra/usr/share/applications/wireshark.desktop"
}