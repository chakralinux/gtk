#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgname=wireshark
pkgver=1.8.6
pkgrel=1
pkgdesc='A free network protocol analyzer for Unix/Linux and Windows'
arch=('x86_64')
license=('GPL2')
depends=('libpcap' 'glib2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('bison' 'flex' 'gtk2' 'krb5' 'libcap' 'libpcap' 'bash' 'gnutls'
             'libgcrypt' 'lua' 'python' 'portaudio')
url='http://www.wireshark.org/'
install=wireshark.install
options=(!libtool)
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2)
sha1sums=('0f51ed901b5e07cceb1373f3368f739be8f1e827')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./autogen.sh
  ./configure \
      --prefix=/extra/usr \
      --with-ssl \
      --with-zlib=yes \
      --with-lua \
      --with-portaudio
  make all
}

package() {

  ### GTK cli ###
  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/extra/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/extra/usr/bin/dumpcap"
  #rm "${pkgdir}/extra/usr/bin/wireshark"

  # Headers
  install -dm755 ${pkgdir}/extra/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

  install -m644 color.h config.h register.h "${pkgdir}/extra/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h ${pkgdir}/extra/usr/include/${pkgbase}/${d}
  done
  
  
  ### GTK GUI ###
  #install -Dm755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/extra/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/extra/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/extra/usr/share/applications/wireshark.desktop"
}