pkgname=breeze-gtk
pkgver=5.5.90.r67.58c4be2
pkgrel=1
pkgdesc="Breeze port for GTK. (GIT version)"
arch=('x86_64')
url='https://quickgit.kde.org/?p=breeze-gtk.git'
license=('LGPL')
depends=('qt5-base')
optdepends=('gtk2: GTK+2 theme'
            'gtk3: GTK+3 theme')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'gtk2')
conflicts=('gnome-breeze')
replaces=('gnome-breeze')
group=(gtk-integration)
source=('git://anongit.kde.org/breeze-gtk.git#commit=58c4be2')
sha1sums=('SKIP')

pkgver(){
  cd breeze-gtk
  _ver="$(cat CMakeLists.txt | grep -m1 -e PROJECT_VERSION | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare(){
  [[ ! -d build ]] && mkdir -p build
}

build(){
  cd build
  cmake ../breeze-gtk \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib

  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
