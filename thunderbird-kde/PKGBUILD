pkgname=thunderbird-kde
pkgver=60.0
pkgrel=1
provides=(thunderbird=${pkgver})
replaces=(thunderbird)
pkgdesc="Standalone Mail/News reader"
arch=('x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird/"
depends=('gtk3' 'gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'libpulse' 'desktop-file-utils' 'hicolor-icon-theme' 'kio' 'knotifications'
         'libvpx' 'icu' 'libevent' 'nss' 'hunspell' 'sqlite3' 'nspr' 'libnotify')
makedepends=('unzip' 'zip' 'python2' 'wireless_tools' 'yasm' 'mesa' 'libpulse'
             'pkg-config' 'gconf' 'xorg-server-xvfb' 'autoconf2.13' 'rust' 'clang' 'llvm')
optdepends=('thunderbird-i18n: for multilanguage support'
	    'libcanberra: for sound support')
options=(!emptydirs !makeflags)
install=thunderbird.install
# For patch look here: https://github.com/manjaro/packages-community/tree/master/thunderbird-kde
source=(https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.xz
        mozconfig
        thunderbird.desktop
        mozilla-kde.patch
        mozilla-nongnome-proxies.patch
        vendor.js
        kde.js
        bz1482248.patch)
sha512sums=('ca3346eb4b6e1237a74ffc5c3d483fcad8d2f0d1146f56a4b11f1ab0f83b47b28fb2da8c0344f228bfff8c84b755f46a86a5b44fd7172de387ceb83650b33152'
            'ee9633832b7cd39cb18967f390691ae11df6981221d47ba6e55ba5ba73f6ade38e66ca7ba98d89aff3b08b88f0ba41e2c610b47b3a369ff99bd3ca00fb27f663'
            '057513bc1b2573f31986916dc905f2e1a165e7500fea51ce7cba1f9f600c0a74396d0d39283ec5ee76fb401133bc614ebcf803b5d15fadac46728d55e30353ea'
            '0fe3c53722708f2675f84bb41dd68319cf6d9af572e0bfc738d9bb6ec2d54ed30381cb237a04379e42254f2b3a143a86bdd9ddcf290316e145f14e8a9d29df9d'
            '2b7b03697e3bf68681fd45eb06235c5f9145ad7f549a3291e0e62fa95e8b3de02a118b0562d50387ff174fbe23a9060f1c749a21cdd2ccb0f20680424f70587b'
            'e9494683d32401745db31f5cfb459be9496c32f5cb9b76e56400c50ad3d40f29affc7863e8c0c1c8e22de51b2a654a838ce33be723b78fe38f77252ce58e063b'
            '06696c5b3216fd38d546d743f61a2ac0bb215a01192e53da75b069095565c602ec5730b3f317de08e4bd3318c4e5642560e9bc25153a69e4b5442772edef8cd8'
            '2a6eae68576495624ba6c5fd8e90255196045f4b07aa11d1a558c87b00ae2f9721ab740c58af68392fc733480731e4a75b30b2c7d921c7ef51424243ee7a3462')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys.
_google_api_key=AIzaSyA33sdKPff0x3KBByOhpzc1i7RS2A1mEpY


# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_mozilla_api_key=bf05f841-e0bd-4644-81f5-3c132755f2e9

prepare(){
  cd thunderbird-${pkgver}
  cp "$srcdir/mozconfig" .mozconfig

  msg "Patching for KDE"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  cd ..

  echo -n "$_google_api_key" > google-api-key
  echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

  echo -n "$_mozilla_api_key" > mozilla-api-key
  echo "ac_add_options --with-mozilla-api-keyfile=\"$PWD/mozilla-api-key\"" >>.mozconfig

  mkdir "$srcdir/path"
  ln -s /usr/bin/python2 "$srcdir/path/python"

  cd comm
  # https://bugzilla.mozilla.org/show_bug.cgi?id=1482248
  patch -p1 -i "$srcdir/bz1482248.patch"
}
         
build() {
  cd thunderbird-${pkgver}

  export PYTHON="/usr/bin/python2"
  export SHELL="/bin/bash"
  
  ./mach configure
  ./mach build
  ./mach buildsymbols
}

package() {
  cd thunderbird-${pkgver}
  DESTDIR="$pkgdir" ./mach install

  install -Dm644 "$srcdir"/vendor.js "$pkgdir/usr/lib/thunderbird/defaults/preferences/vendor.js"
  install -Dm644 "$srcdir"/kde.js "$pkgdir/usr/lib/thunderbird/defaults/preferences/kde.js"

  _distini="$pkgdir/usr/lib/thunderbird/distribution/distribution.ini"
  install -Dm644 /dev/stdin "$_distini" <<END
[Global]
id=chakralinux
version=1.0
about=Mozilla Thunderbird for Chakra GNU/Linux

[Preferences]
app.distributor=chakralinux
app.distributor.channel=${pkgname%-kde}
app.partner.chakralinux=chakralinux
END

  for i in 16 22 24 32 48 64 128 256; do
      install -Dm644 comm/mail/branding/thunderbird/default${i}.png \
          "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/thunderbird.png"
  done

  install -Dm644 comm/mail/branding/thunderbird/TB-symbolic.svg \
    "$pkgdir/usr/share/icons/hicolor/symbolic/apps/thunderbird-symbolic.svg"

  install -Dm644 "$srcdir/thunderbird.desktop" \
      "$pkgdir/usr/share/applications/thunderbird.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir/usr/lib/thunderbird/{dictionaries,hyphenation}"
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/thunderbird/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/thunderbird/hyphenation"

  ln -sf thunderbird "$pkgdir/usr/lib/thunderbird/thunderbird-bin"
}
