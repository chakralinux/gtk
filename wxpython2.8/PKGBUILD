# Maintainer: Jeff Haung <s8321414[at]gmail[dot]com>
# Original maintainer on CCR: Kyle Devir (QuartzDragon) <kyle.devir@gmx.com>

pkgname=wxpython2.8
pkgver=2.8.12.1
pkgrel=8
pkgdesc="A wxWidgets GUI toolkit for Python"
arch=('x86_64')
license=('custom:wxWindows')
url="http://www.wxpython.org"
depends=('wxgtk2.8' 'wxpython' 'python2')
makedepends=('mesa' 'glu')
provides=('wxpython2.8')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2
        "wxpython-cairo.patch"
        "wxpython-fpb_default_style.patch"
        "wxGTK-collision.patch")
sha512sums=('4f826ccb57d5f4fc03fe7cae67499be70c727a99aebf9d153ea1060a21641642606d44d079ba897c38b176846336aeaebc21d30d5c7a0abec3934d65cf4529f3'
            '015f384be9029ae12319f22aadf81d55c4dbb76fbe2d3890d3a2b4ff93a909d678fb7c6149d5b5494d1bc180b2bd27328b0a763fef6f0af3dee776d3e277f4ed'
            '374ddba137881724cbf11188fed1c76451573689dad09182e5b990549d1a48f1d814b78b3d7001c4a50fd85963aa63b98c6fd318433ed4e322a7ee0c75c0f8e4'
            '8cad5a63024ffb275dfb9a38f824fe018702cdf7dd250d06d9d90b0d150e074410a36b62da2629fb794f4bb1f3c063f1e516bfbe9c8b3cddeae7c4bfba51e835')

# http://wiki.wxpython.org/MultiVersionInstalls
# python program must have this snipet code added to force 2.8 usage:
# import wxversion
# wxversion.select("2.8")

build() {
    cd "${srcdir}/wxPython-src-${pkgver}"

    sed -i 's,/usr/$wx_cv_std_libpath,& /usr/lib,' configure
    patch -p1 -i ../wxGTK-collision.patch
    find . -type f -exec sed -i 's/env python/env python2/' {} \;

    ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --with-gtk=2 \
              --with-opengl \
              --enable-unicode \
              --enable-graphics_ctx \
              --disable-optimize \
              --enable-mediactrl \
              --with-libpng=sys \
              --with-libxpm=sys \
              --with-libjpeg=sys \
              --with-libtiff=sys \
              --with-gnomeprint \
              --disable-precomp-headers

    cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
    patch -p2 -i "${srcdir}/wxpython-cairo.patch"
    patch -p1 -i "${srcdir}/wxpython-fpb_default_style.patch"
    python2 setup.py WXPORT=gtk2 UNICODE=1 WX_CONFIG=/usr/bin/wx-config-2.8 build
}
 
package() {
    cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
    
    python2 setup.py WXPORT=gtk2 UNICODE=1 WX_CONFIG=/usr/bin/wx-config-2.8 install --root="${pkgdir}"
    
    rm -rf "${pkgdir}/usr/bin"
    rm "${pkgdir}"/usr/lib/python2.7/site-packages/{wx.pth,wxversion.py*}
    
    install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}