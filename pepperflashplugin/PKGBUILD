pkgname=pepperflashplugin
pkgdesc="Adobe/Google Pepper Flash Player plugin installer"
pkgver=28.0.0.126
pkgrel=1
arch=('x86_64')
url="http://www.adobe.com/downloads.html"
license=('custom')
source=("https://fpdownload.adobe.com/pub/flashplayer/pdc/$pkgver/flash_player_ppapi_linux.x86_64.tar.gz"
        "http://wwwimages.adobe.com/content/dam/acom/en/legal/licenses-terms/pdf/Flash_Player_${pkgver%%.*}_0.pdf"
        'chromium_pepperflashplayer.sh')
sha1sums=('1cc5999da0c7d5d92619a5768d08e30aef570c5e'
          '1eee898b48a1c6f6dd9bdfd821dffc14dcf8079f'
          '9de6c84623820f3de10df6e33274794dfb808a01')


package() {
  # install flashplayer plugin
  install -Dm644 libpepflashplayer.so manifest.json -t $pkgdir/usr/lib/PepperFlash/

  # copy the script to enable pepperflash on chromium
  install -Dm644 "$srcdir/chromium_pepperflashplayer.sh" "$pkgdir/etc/chromium/default"
  sed -i "s/\@FLASH_VERSION\@/$pkgver/" "$pkgdir/etc/chromium/default"

  # copy license
  install -Dm644 "${srcdir}"/readme.txt "${pkgdir}"/usr/share/licenses/${pkgname}/README
  install -Dm644 $srcdir/LGPL/LGPL.txt $pkgdir/usr/share/licenses/$pkgname/LPGL.txt
  install -Dm644 Flash_Player_${pkgver%%.*}_0.pdf -t "$pkgdir/usr/share/licenses/$pkgname/"
}
