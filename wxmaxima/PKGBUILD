pkgname=wxmaxima
pkgver=13.04.0
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('x86_64')
url="http://wxmaxima.sourceforge.net/"
license=('GPL2')
depends=('maxima' 'libxml2' 'wxgtk' 'shared-mime-info' 'desktop-file-utils' 'filesystem-extra')
source=(http://downloads.sourceforge.net/$pkgname/wxMaxima-$pkgver.tar.gz
	Makefile_locale_zh_CN.patch)
install=wxmaxima.install
sha1sums=('1dac390257a4c8424781c6b8e7d1d22ee2024449'
	  'e2004670c38dc2ef65586e89e1246e14dca232c2')

build() {
  cd "${srcdir}/wxMaxima-${pkgver}"
  
  patch -p1 -i "$srcdir/Makefile_locale_zh_CN.patch"
  
  ./configure --prefix=/extra/usr
  make
}

package() {
  cd "${srcdir}/wxMaxima-${pkgver}"

  make DESTDIR="${pkgdir}" install 

  # Fix category in .desktop file
  sed -i -e 's/Utility;X-Red-Hat-Base;X-Red-Hat-Base-Only;/Science;Math;/' wxmaxima.desktop

  # Install desktop file and icon
  install -m755 -d "${pkgdir}/extra/usr/share/applications"
  install -m755 -d "${pkgdir}/extra/usr/share/pixmaps"
  install -m644 wxmaxima.desktop "${pkgdir}/extra/usr/share/applications/" 
  install -m644 data/wxmaxima.png "${pkgdir}/extra/usr/share/pixmaps/" 
}
