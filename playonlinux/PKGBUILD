# Maintainer: Jeff Haung <s8321414[at]gmail[dot]com>
# Original maintainer on CCR: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=playonlinux
pkgver=4.2.5
pkgrel=2
pkgdesc="GUI for managing Windows programs under linux"
url="http://www.playonlinux.com/"
license=('GPL')
depends=('wine' 'imagemagick' 'xterm' 'wxpython2.8' 'cabextract' 'unzip' 'mesa-demos'
         'xdg-user-dirs' 'gnu-netcat' 'gnupg' 'icoutils' 'xdg-user-dirs' 'libxmu')
arch=('any')
source=(http://www.playonlinux.com/script_files/PlayOnLinux/${pkgver/.0/}/PlayOnLinux_${pkgver/.0/}.tar.gz
        PlayOnLinux.desktop
        use-python2.patch)
options=(!strip)
md5sums=('35e3a7f88da2f6b1cbc890d219fe5f71'
         'ad300503f8937ca93eae9180687d4e88'
         '613ac5b3efefd2ea769e1b66a7c7b804')
screenshot=('http://i.imgur.com/77mGY.png')

package() {
  cd "$srcdir/$pkgname"

  patch -Np1 -i ../use-python2.patch

  install -d $pkgdir/usr/share/$pkgname
  install -d $pkgdir/usr/bin 
  cp -r $srcdir/$pkgname/ $pkgdir/usr/share/

  echo "#!/bin/bash" > $pkgdir/usr/bin/$pkgname 
  echo "/usr/share/$pkgname/$pkgname \"\$@\"" >> $pkgdir/usr/bin/$pkgname
  chmod +x  $pkgdir/usr/bin/$pkgname
  
  # Needed for wxpython2.8 support, wxpython 3.0 is buggy with POL
  sed -i "s/wxversion.ensureMinimal/wxversion.select/g" $pkgdir/usr/share/$pkgname/python/mainwindow.py
  
  install -D -m644 $srcdir/$pkgname/etc/playonlinux32.png $pkgdir/usr/share/pixmaps/playonlinux32.png 
  install -D -m644 $srcdir/PlayOnLinux.desktop $pkgdir/usr/share/applications/playonlinux.desktop 
}

